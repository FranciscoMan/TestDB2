CREATE TABLE [ADM].[C_POSITION] (
    [ID_POSITION]          INT            IDENTITY (1, 1) NOT NULL,
    [KY_POSITION]          NVARCHAR (50)  NOT NULL,
    [NM_POSITION]          NVARCHAR (300) NOT NULL,
    [DS_POSITION]          NVARCHAR (500) NULL,
    [XML_PHONE]            XML            NULL,
    [KY_EMAIL]             NVARCHAR (500) NULL,
    [KY_TELEGRAM]          NVARCHAR (15)  NULL,
    [ID_BRANCH_PLANT]      INT            NULL,
    [DS_AREA]              NVARCHAR (100) NULL,
    [ID_DEPARTMENT]        INT            NULL,
    [DT_CREATION]          DATETIME       NOT NULL,
    [DT_UPDATE]            DATETIME       NULL,
    [KY_USER_APP_CREATION] NVARCHAR (50)  NOT NULL,
    [KY_USER_APP_UPDATE]   NVARCHAR (50)  NULL,
    [NM_PROGAM_CREATE]     NVARCHAR (50)  NOT NULL,
    [NM_PROGRAM_UPDATE]    NVARCHAR (50)  NULL,
    CONSTRAINT [PK_C_POSITION] PRIMARY KEY CLUSTERED ([ID_POSITION] ASC),
    CONSTRAINT [FK_C_POSITION_C_BRANCH_PLANT] FOREIGN KEY ([ID_BRANCH_PLANT]) REFERENCES [ADM].[C_BRANCH_PLANT] ([ID_BRANCH_PLANT]),
    CONSTRAINT [FK_C_POSITION_C_DEPARTMENT] FOREIGN KEY ([ID_DEPARTMENT]) REFERENCES [ADM].[C_DEPARTMENT] ([ID_DEPARTMENT])
);


GO
CREATE TRIGGER [ADM].[TR_CUD_POSITION] ON  [ADM].[C_POSITION] AFTER INSERT,UPDATE,DELETE
AS 
BEGIN

DECLARE @XML_CATALOG AS XML,
		@XML_VALUES AS XML,
		@XML_REFERENCE AS XML,
		@KY_CATALOG AS NVARCHAR(100) = 'POSITION',
		@DS_MESSAGE AS NVARCHAR(100),
		@KY_ACTION NVARCHAR(10),
		@KY_ACTION_INSERT NVARCHAR(10) = 'INSERT',
		@KY_ACTION_UPDATE NVARCHAR(10) = 'UPDATE',
		@KY_ACTION_DELETE NVARCHAR(10) = 'DELETE',
		@NO_AFFECTED_RECORDS INT = 0,
		@KY_USER NVARCHAR(50),
		@NM_PROGRAM NVARCHAR(50),
		@DT_AFFECTED DATETIME
BEGIN TRY

	SET @KY_ACTION = (
		SELECT CASE 
			WHEN EXISTS(SELECT TOP 1 1 FROM inserted) AND EXISTS(SELECT TOP 1 1 FROM deleted) THEN @KY_ACTION_UPDATE --IF SO, THEN UPDATE
			WHEN EXISTS(SELECT TOP 1 1 FROM inserted) THEN @KY_ACTION_INSERT
			WHEN EXISTS(SELECT TOP 1 1 FROM deleted) THEN @KY_ACTION_DELETE
		END
	)

	------------------------------------------------ UPDATE
	IF @KY_ACTION = @KY_ACTION_UPDATE BEGIN


		SET @XML_VALUES = (
			SELECT 
				I.ID_POSITION AS "@ID_POSITION" 
				, I.KY_POSITION AS "@KY_POSITION_NEW"
				, I.NM_POSITION AS "@NM_POSITION_NEW"
				, I.DS_POSITION AS "@DS_POSITION_NEW"
				, I.KY_EMAIL AS "@KY_EMAIL_NEW"
				, I.KY_TELEGRAM AS "@KY_TELEGRAM_NEW"
				, I.ID_BRANCH_PLANT AS "@ID_BRANCH_PLANT_NEW"
				, D.KY_POSITION AS "@KY_POSITION_OLD"
				, D.NM_POSITION AS "@NM_POSITION_OLD"
				, D.DS_POSITION AS "@DS_POSITION_OLD"
				, D.KY_EMAIL AS "@KY_EMAIL_OLD"
				, D.KY_TELEGRAM AS "@KY_TELEGRAM_OLD"
				, D.ID_BRANCH_PLANT AS "@ID_BRANCH_PLANT_OLD"
				FROM inserted I
					INNER JOIN deleted D
						ON I.ID_POSITION = D.ID_POSITION
			FOR XML PATH ('UPDATED')
		)

		SELECT @NO_AFFECTED_RECORDS = COUNT(*) FROM inserted
		SELECT TOP 1 @DS_MESSAGE = 'The position ' + NM_POSITION + ', with id ' + CONVERT( NVARCHAR(10),ID_POSITION) + ' has been updated correctly' 
			, @KY_USER = KY_USER_APP_UPDATE
			, @NM_PROGRAM = NM_PROGRAM_UPDATE
			, @DT_AFFECTED = DT_UPDATE
		FROM inserted

		IF @NO_AFFECTED_RECORDS > 1
			SET @DS_MESSAGE = CONVERT(NVARCHAR(10), @NO_AFFECTED_RECORDS) + ' positions has been updated correctly'

	END

	-------------------------------------------------- INSERT
	IF @KY_ACTION = @KY_ACTION_INSERT BEGIN
		SET @XML_VALUES = (
			SELECT 
				  I.ID_POSITION AS "@ID_POSITION" 
				, I.KY_POSITION AS "@KY_POSITION_NEW"
				, I.NM_POSITION AS "@NM_POSITION_NEW"
				, I.DS_POSITION AS "@DS_POSITION_NEW"
				, I.KY_EMAIL AS "@KY_EMAIL_NEW"
				, I.KY_TELEGRAM AS "@KY_TELEGRAM_NEW"
				, I.ID_BRANCH_PLANT AS "@ID_BRANCH_PLANT_NEW"
				FROM inserted i
			FOR XML PATH ('INSERTED')
		)
		
		SELECT @NO_AFFECTED_RECORDS = COUNT(*) FROM inserted
		SELECT TOP 1 @DS_MESSAGE = 'The position ' + NM_POSITION + ', with id ' + CONVERT( NVARCHAR(10),ID_POSITION) + ' has been inserted correctly' 
			, @KY_USER = KY_USER_APP_CREATION
			, @NM_PROGRAM = NM_PROGAM_CREATE
			, @DT_AFFECTED = DT_CREATION
		FROM inserted

		IF @NO_AFFECTED_RECORDS > 1
			SET @DS_MESSAGE = CONVERT(NVARCHAR(10), @NO_AFFECTED_RECORDS) + ' positions has been inserted correctly'
	END

	------------------------------------------------ DELETE
	IF @KY_ACTION = @KY_ACTION_DELETE BEGIN
		SET @XML_VALUES = (
			SELECT 
			      D.ID_POSITION AS "@ID_POSITION" 
				, D.KY_POSITION AS "@KY_POSITION_OLD"
				, D.NM_POSITION AS "@NM_POSITION_OLD"
				, D.DS_POSITION AS "@DS_POSITION_OLD"
				, D.KY_EMAIL AS "@KY_EMAIL_OLD"
				, D.KY_TELEGRAM AS "@KY_TELEGRAM_OLD"
				, D.ID_BRANCH_PLANT AS "@ID_BRANCH_PLANT_OLD"
				FROM deleted D
			FOR XML PATH ('DELETED')
		)
		
		SELECT @NO_AFFECTED_RECORDS = COUNT(*) FROM deleted
		SELECT TOP 1 @DS_MESSAGE = 'The position ' + NM_POSITION + ', with id ' + CONVERT( NVARCHAR(10),ID_POSITION) + ' has been deleted correctly' 
			, @KY_USER = KY_USER_APP_CREATION
			, @NM_PROGRAM = NM_PROGAM_CREATE
			, @DT_AFFECTED = GETDATE()
		FROM deleted

		IF @NO_AFFECTED_RECORDS > 1
			SET @DS_MESSAGE = CONVERT(NVARCHAR(10), @NO_AFFECTED_RECORDS) + ' positions has been deleted correctly'
	END

	SET @XML_CATALOG = (
		SELECT  
			@KY_CATALOG AS "@CATALOG" 
			, @XML_VALUES
		FOR XML PATH ('CATALOG')
	)
	
	SET @XML_REFERENCE = (
		SELECT  
			@XML_CATALOG
		FOR XML PATH ('REFERENCE')
	)


	INSERT INTO [PRD].[K_LOG]([DS_LOG],[XML_REFERENCE],DT_CREATION,KY_USER_APP_CREATION,NM_PROGAM_CREATE)
	SELECT @DS_MESSAGE, @XML_REFERENCE, @DT_AFFECTED, @KY_USER, @NM_PROGRAM;

END TRY
BEGIN CATCH  
	ROLLBACK;  
END CATCH; 
	
END