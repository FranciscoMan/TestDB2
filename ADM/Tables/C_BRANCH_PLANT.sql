CREATE TABLE [ADM].[C_BRANCH_PLANT] (
    [ID_BRANCH_PLANT]              INT            IDENTITY (1, 1) NOT NULL,
    [KY_BRANCH_PLANT]              NVARCHAR (50)  NOT NULL,
    [NM_BRANCH_PLANT]              VARCHAR (300)  NULL,
    [DS_BRANCH_PLANT]              NVARCHAR (500) NULL,
    [ID_FILE]                      NVARCHAR (255) NULL,
    [ID_TIME_ZONE]                 NVARCHAR (255) NULL,
    [TZ_OFFSET]                    INT            CONSTRAINT [DF_C_BRANCH_PLANT_TZ_OFFSET] DEFAULT ((0)) NOT NULL,
    [FG_QUALITY_INSPECTOR]         BIT            CONSTRAINT [DF_C_BRANCH_PLANT_FG_QUALITY_INSPECTOR] DEFAULT ((0)) NOT NULL,
    [ID_LENGTH_METRIC]             INT            NULL,
    [NO_LENGTH_MIN_VALUE]          FLOAT (53)     NULL,
    [NO_LENGTH_MAX_VALUE]          FLOAT (53)     NULL,
    [ID_WIDTH_METRIC]              INT            NULL,
    [NO_WIDTH_MIN_VALUE]           FLOAT (53)     NULL,
    [NO_WIDTH_MAX_VALUE]           FLOAT (53)     NULL,
    [ID_WEIGHT_METRIC]             INT            NULL,
    [NO_WEIGHT_MIN_VALUE]          FLOAT (53)     NULL,
    [NO_WEIGHT_MAX_VALUE]          FLOAT (53)     NULL,
    [ID_THICKNESS_METRIC]          INT            NULL,
    [ID_LIGHT_TRANSMISSION_METRIC] INT            NULL,
    [ID_GLOSS_METRIC]              INT            NULL,
    [ID_CUT_TIME_METRIC]           INT            NULL,
    [ID_TOTAL_CUT_WEIGHT_METRIC]   INT            NULL,
    [ID_LBS_PER_HOUR_METRIC]       INT            NULL,
    [ID_PARTS_EXT_METRIC]          INT            NULL,
    [ID_WEB_WIDTH_METRIC]          INT            NULL,
    [ID_DIE_PUMP_METRIC]           INT            NULL,
    [ID_CARTON_ITEM_CATALOG]       INT            NULL,
    [DT_CREATION]                  DATETIME       NULL,
    [DT_UPDATE]                    DATETIME       NULL,
    [KY_USER_APP_CREATION]         NVARCHAR (50)  NOT NULL,
    [KY_USER_APP_UPDATE]           NVARCHAR (50)  NULL,
    [NM_PROGAM_CREATE]             NVARCHAR (50)  NOT NULL,
    [NM_PROGRAM_UPDATE]            NVARCHAR (50)  NULL,
    CONSTRAINT [PK_C_BRANCH_PLANT] PRIMARY KEY CLUSTERED ([ID_BRANCH_PLANT] ASC)
);


GO
CREATE TRIGGER [ADM].[TR_CUD_BRANCH_PLANT] ON  ADM.C_BRANCH_PLANT AFTER INSERT,UPDATE,DELETE
AS 
BEGIN

DECLARE @XML_CATALOG AS XML,
		@XML_VALUES AS XML,
		@XML_REFERENCE AS XML,
		@KY_CATALOG AS NVARCHAR(100) = 'BRANCH PLANT',
		@DS_MESSAGE AS NVARCHAR(100),
		@KY_ACTION NVARCHAR(10),
		@KY_ACTION_INSERT NVARCHAR(10) = 'INSERT',
		@KY_ACTION_UPDATE NVARCHAR(10) = 'UPDATE',
		@KY_ACTION_DELETE NVARCHAR(10) = 'DELETE',
		@NO_AFFECTED_RECORDS INT = 0,
		@KY_USER NVARCHAR(50),
		@NM_PROGRAM NVARCHAR(50),
		@DT_AFFECTED DATETIME
BEGIN TRY

	SET @KY_ACTION = (
		SELECT CASE 
			WHEN EXISTS(SELECT TOP 1 1 FROM inserted) AND EXISTS(SELECT TOP 1 1 FROM deleted) THEN @KY_ACTION_UPDATE --IF SO, THEN UPDATE
			WHEN EXISTS(SELECT TOP 1 1 FROM inserted) THEN @KY_ACTION_INSERT
			WHEN EXISTS(SELECT TOP 1 1 FROM deleted) THEN @KY_ACTION_DELETE
		END
	)

	------------------------------------------------ UPDATE
	IF @KY_ACTION = @KY_ACTION_UPDATE BEGIN


		SET @XML_VALUES = (
			SELECT 
				  I.ID_BRANCH_PLANT AS "@ID_BRANCH_PLANT" 
				, I.KY_BRANCH_PLANT AS "@KY_BRANCH_PLANT_NEW"
				, I.NM_BRANCH_PLANT AS "@NM_BRANCH_PLANT_NEW"
				, I.DS_BRANCH_PLANT AS "@DS_BRANCH_PLANT_NEW"
				, I.ID_FILE AS "@ID_FILE_NEW"
				, D.KY_BRANCH_PLANT AS "@KY_BRANCH_PLANT_OLD"
				, D.NM_BRANCH_PLANT AS "@NM_BRANCH_PLANT_OLD"
				, D.DS_BRANCH_PLANT AS "@DS_BRANCH_PLANT_OLD"
				, D.ID_FILE AS "@ID_FILE_OLD"
				FROM inserted I
					INNER JOIN deleted D
						ON I.ID_BRANCH_PLANT = D.ID_BRANCH_PLANT
			FOR XML PATH ('UPDATED')
		)

		SELECT @NO_AFFECTED_RECORDS = COUNT(*) FROM inserted
		SELECT TOP 1 @DS_MESSAGE = 'The branch plant ' + NM_BRANCH_PLANT + ', with id ' + CONVERT( NVARCHAR(10),ID_BRANCH_PLANT) + ' has been updated correctly' 
			, @KY_USER = KY_USER_APP_UPDATE
			, @NM_PROGRAM = NM_PROGRAM_UPDATE
			, @DT_AFFECTED = DT_UPDATE
		FROM inserted

		IF @NO_AFFECTED_RECORDS > 1
			SET @DS_MESSAGE = CONVERT(NVARCHAR(10), @NO_AFFECTED_RECORDS) + ' branch plants has been updated correctly'

	END

	-------------------------------------------------- INSERT
	IF @KY_ACTION = @KY_ACTION_INSERT BEGIN
		SET @XML_VALUES = (
			SELECT 
				  I.ID_BRANCH_PLANT AS "@ID_BRANCH_PLANT" 
				, I.KY_BRANCH_PLANT AS "@KY_BRANCH_PLANT_NEW"
				, I.NM_BRANCH_PLANT AS "@NM_BRANCH_PLANT_NEW"
				, I.DS_BRANCH_PLANT AS "@DS_BRANCH_PLANT_NEW"
				, I.ID_FILE AS "@ID_FILE_NEW"
				FROM inserted i
			FOR XML PATH ('INSERTED')
		)
		
		SELECT @NO_AFFECTED_RECORDS = COUNT(*) FROM inserted
		SELECT TOP 1 @DS_MESSAGE = 'The branch plant ' + NM_BRANCH_PLANT + ', with id ' + CONVERT( NVARCHAR(10),ID_BRANCH_PLANT) + ' has been inserted correctly' 
			, @KY_USER = KY_USER_APP_CREATION
			, @NM_PROGRAM = NM_PROGAM_CREATE
			, @DT_AFFECTED = DT_CREATION
		FROM inserted

		IF @NO_AFFECTED_RECORDS > 1
			SET @DS_MESSAGE = CONVERT(NVARCHAR(10), @NO_AFFECTED_RECORDS) + ' branch plants has been inserted correctly'
	END

	------------------------------------------------ DELETE
	IF @KY_ACTION = @KY_ACTION_DELETE BEGIN
		SET @XML_VALUES = (
			SELECT 
				  D.ID_BRANCH_PLANT AS "@ID_BRANCH_PLANT" 
				, D.KY_BRANCH_PLANT AS "@KY_BRANCH_PLANT_OLD"
				, D.NM_BRANCH_PLANT AS "@NM_BRANCH_PLANT_OLD"
				, D.DS_BRANCH_PLANT AS "@DS_BRANCH_PLANT_OLD"
				, D.ID_FILE AS "@ID_FILE_OLD"
				FROM deleted D
			FOR XML PATH ('DELETED')
		)
		
		SELECT @NO_AFFECTED_RECORDS = COUNT(*) FROM deleted
		SELECT TOP 1 @DS_MESSAGE = 'The branch plant ' + NM_BRANCH_PLANT + ', with id ' + CONVERT( NVARCHAR(10),ID_BRANCH_PLANT) + ' has been deleted correctly' 
			, @KY_USER = KY_USER_APP_CREATION
			, @NM_PROGRAM = NM_PROGAM_CREATE
			, @DT_AFFECTED = GETDATE()
		FROM deleted

		IF @NO_AFFECTED_RECORDS > 1
			SET @DS_MESSAGE = CONVERT(NVARCHAR(10), @NO_AFFECTED_RECORDS) + ' branch plants has been deleted correctly'
	END

	SET @XML_CATALOG = (
		SELECT  
			@KY_CATALOG AS "@CATALOG" 
			, @XML_VALUES
		FOR XML PATH ('CATALOG')
	)
	
	SET @XML_REFERENCE = (
		SELECT  
			@XML_CATALOG
		FOR XML PATH ('REFERENCE')
	)


	INSERT INTO [PRD].[K_LOG]([DS_LOG],[XML_REFERENCE],DT_CREATION,KY_USER_APP_CREATION,NM_PROGAM_CREATE)
	SELECT @DS_MESSAGE, @XML_REFERENCE, @DT_AFFECTED, @KY_USER, @NM_PROGRAM;

END TRY
BEGIN CATCH  
	ROLLBACK;  
END CATCH; 
	
END