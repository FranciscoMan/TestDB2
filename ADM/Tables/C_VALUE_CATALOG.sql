CREATE TABLE [ADM].[C_VALUE_CATALOG] (
    [ID_VALUE_CATALOG]     INT             IDENTITY (1, 1) NOT NULL,
    [KY_VALUE_CATALOG]     NVARCHAR (50)   NOT NULL,
    [NM_VALUE_CATALOG]     NVARCHAR (100)  NOT NULL,
    [DS_VALUE_CATALOG]     NVARCHAR (1000) NULL,
    [ID_LIST_CATALOG]      INT             NOT NULL,
    [FG_ACTIVE]            BIT             CONSTRAINT [DF_C_VALUE_CATALOG_FG_ACTIVE] DEFAULT ((1)) NOT NULL,
    [DT_CREATION]          DATETIME        NOT NULL,
    [DT_UPDATE]            DATETIME        NULL,
    [KY_USER_APP_CREATION] NVARCHAR (50)   NOT NULL,
    [KY_USER_APP_UPDATE]   NVARCHAR (50)   NULL,
    [NM_PROGAM_CREATE]     NVARCHAR (50)   NOT NULL,
    [NM_PROGRAM_UPDATE]    NVARCHAR (50)   NULL,
    [ID_BRANCH_PLANT]      INT             NULL,
    CONSTRAINT [PK_C_VALUE_CATALOG] PRIMARY KEY CLUSTERED ([ID_VALUE_CATALOG] ASC),
    FOREIGN KEY ([ID_BRANCH_PLANT]) REFERENCES [ADM].[C_BRANCH_PLANT] ([ID_BRANCH_PLANT]),
    CONSTRAINT [FK_C_VALUE_CATALOG_C_LIST_CATALOG] FOREIGN KEY ([ID_LIST_CATALOG]) REFERENCES [ADM].[C_LIST_CATALOG] ([ID_LIST_CATALOG]),
    CONSTRAINT [UC_ID_LIST_CATALOG_KY_VALUE_CATALOG] UNIQUE NONCLUSTERED ([ID_LIST_CATALOG] ASC, [KY_VALUE_CATALOG] ASC)
);


GO

CREATE TRIGGER [ADM].[TR_CUD_VALUE_CATALOG] ON  [ADM].[C_VALUE_CATALOG] AFTER INSERT,UPDATE,DELETE
AS 
BEGIN

DECLARE @XML_CATALOG AS XML,
		@XML_VALUES AS XML,
		@XML_REFERENCE AS XML,
		@KY_CATALOG AS NVARCHAR(100) = 'VALUE_CATALOG',
		@DS_MESSAGE AS NVARCHAR(100),
		@KY_ACTION NVARCHAR(10),
		@KY_ACTION_INSERT NVARCHAR(10) = 'INSERT',
		@KY_ACTION_UPDATE NVARCHAR(10) = 'UPDATE',
		@KY_ACTION_DELETE NVARCHAR(10) = 'DELETE',
		@NO_AFFECTED_RECORDS INT = 0,
		@KY_USER NVARCHAR(50),
		@NM_PROGRAM NVARCHAR(50),
		@DT_AFFECTED DATETIME
BEGIN TRY

	SET @KY_ACTION = (
		SELECT CASE 
			WHEN EXISTS(SELECT TOP 1 1 FROM inserted) AND EXISTS(SELECT TOP 1 1 FROM deleted) THEN @KY_ACTION_UPDATE --IF SO, THEN UPDATE
			WHEN EXISTS(SELECT TOP 1 1 FROM inserted) THEN @KY_ACTION_INSERT
			WHEN EXISTS(SELECT TOP 1 1 FROM deleted) THEN @KY_ACTION_DELETE
		END
	)

	------------------------------------------------ UPDATE
	IF @KY_ACTION = @KY_ACTION_UPDATE BEGIN

		SET @XML_VALUES = (
			SELECT 
				  I.ID_VALUE_CATALOG AS "@ID_VALUE_CATALOG" 
				, I.KY_VALUE_CATALOG AS "@KY_VALUE_CATALOG_NEW"
				, I.NM_VALUE_CATALOG AS "@NM_VALUE_CATALOG_NEW"
				, I.DS_VALUE_CATALOG AS "@DS_VALUE_CATALOG_NEW"
				, I.ID_LIST_CATALOG AS "@ID_LIST_CATALOG_NEW"
				, I.FG_ACTIVE AS "@FG_ACTIVE_NEW"
				, D.KY_VALUE_CATALOG AS "@KY_VALUE_CATALOG_OLD"
				, D.NM_VALUE_CATALOG AS "@NM_VALUE_CATALOG_OLD"
				, D.DS_VALUE_CATALOG AS "@DS_VALUE_CATALOG_OLD"
				, D.ID_LIST_CATALOG AS "@ID_LIST_CATALOG_OLD"
				, D.FG_ACTIVE AS "@FG_ACTIVE_OLD"
				FROM inserted I
					INNER JOIN deleted D
						ON I.ID_VALUE_CATALOG = D.ID_VALUE_CATALOG
			FOR XML PATH ('UPDATED')
		)

		SELECT @NO_AFFECTED_RECORDS = COUNT(*) FROM inserted
		SELECT TOP 1 @DS_MESSAGE = 'The value ''' + I.NM_VALUE_CATALOG + ''' with id ' + CONVERT(NVARCHAR(10), I.ID_VALUE_CATALOG) + ' from ''' + CLC.NM_LIST_CATALOG + ''' catalog has been updated correctly' 
			, @KY_USER = I.KY_USER_APP_UPDATE
			, @NM_PROGRAM = I.NM_PROGRAM_UPDATE
			, @DT_AFFECTED = I.DT_UPDATE
		FROM inserted I
			INNER JOIN ADM.C_LIST_CATALOG CLC
				ON I.ID_LIST_CATALOG = CLC.ID_LIST_CATALOG

		IF @NO_AFFECTED_RECORDS > 1
			SET @DS_MESSAGE = CONVERT(NVARCHAR(10), @NO_AFFECTED_RECORDS) + ' catalog values has been updated correctly'

	END

	-------------------------------------------------- INSERT
	IF @KY_ACTION = @KY_ACTION_INSERT BEGIN
		SET @XML_VALUES = (
		   SELECT I.ID_VALUE_CATALOG AS "@ID_VALUE_CATALOG" 
				, I.ID_VALUE_CATALOG AS "@KY_VALUE_CATALOG_NEW"
				, I.NM_VALUE_CATALOG AS "@NM_VALUE_CATALOG_NEW"
				, I.DS_VALUE_CATALOG AS "@DS_VALUE_CATALOG_NEW"
				, I.ID_LIST_CATALOG AS "@ID_LIST_CATALOG_NEW"
				, I.FG_ACTIVE AS "@FG_ACTIVE_NEW"
				FROM inserted I
			FOR XML PATH ('INSERTED')
		)
		
		SELECT @NO_AFFECTED_RECORDS = COUNT(*) FROM inserted
		SELECT TOP 1 @DS_MESSAGE = 'The value ''' + I.NM_VALUE_CATALOG + ''' with id ' + CONVERT(NVARCHAR(10), I.ID_VALUE_CATALOG) + ' from ''' + CLC.NM_LIST_CATALOG + ''' catalog has been inserted correctly' 
			, @KY_USER = I.KY_USER_APP_CREATION
			, @NM_PROGRAM = I.NM_PROGAM_CREATE
			, @DT_AFFECTED = GETDATE()
		FROM inserted I
			INNER JOIN ADM.C_LIST_CATALOG CLC
				ON I.ID_LIST_CATALOG = CLC.ID_LIST_CATALOG

		IF @NO_AFFECTED_RECORDS > 1
			SET @DS_MESSAGE = CONVERT(NVARCHAR(10), @NO_AFFECTED_RECORDS) + ' catalog values has been inserted correctly'
	END

	------------------------------------------------ DELETE
	IF @KY_ACTION = @KY_ACTION_DELETE BEGIN
		SET @XML_VALUES = (
		   SELECT D.ID_VALUE_CATALOG AS "@ID_VALUE_CATALOG" 
				, D.KY_VALUE_CATALOG AS "@KY_VALUE_CATALOG_OLD"
				, D.NM_VALUE_CATALOG AS "@NM_VALUE_CATALOG_OLD"
				, D.DS_VALUE_CATALOG AS "@DS_VALUE_CATALOG_OLD"
				, D.ID_LIST_CATALOG AS "@ID_LIST_CATALOG_OLD"
				, D.FG_ACTIVE AS "@FG_ACTIVE_OLD"
				FROM deleted D
			FOR XML PATH ('DELETED')
		)
		
		SELECT @NO_AFFECTED_RECORDS = COUNT(*) FROM deleted
		SELECT TOP 1 @DS_MESSAGE = 'The value ''' + D.NM_VALUE_CATALOG + ''' with id ' + CONVERT(NVARCHAR(10), D.ID_VALUE_CATALOG) + ' from ''' + CLC.NM_LIST_CATALOG + ''' catalog has been deleted correctly' 
			, @KY_USER = D.KY_USER_APP_CREATION
			, @NM_PROGRAM = D.NM_PROGAM_CREATE
			, @DT_AFFECTED = GETDATE()
		FROM deleted D
			INNER JOIN ADM.C_LIST_CATALOG CLC
				ON D.ID_LIST_CATALOG = CLC.ID_LIST_CATALOG

		IF @NO_AFFECTED_RECORDS > 1
			SET @DS_MESSAGE = CONVERT(NVARCHAR(10), @NO_AFFECTED_RECORDS) + ' catalog values has been deleted correctly'
	END

	SET @XML_CATALOG = (
		SELECT  
			@KY_CATALOG AS "@CATALOG" 
			, @XML_VALUES
		FOR XML PATH ('CATALOG')
	)
	
	SET @XML_REFERENCE = (
		SELECT  
			@XML_CATALOG
		FOR XML PATH ('REFERENCE')
	)


	INSERT INTO [PRD].[K_LOG]([DS_LOG],[XML_REFERENCE],DT_CREATION,KY_USER_APP_CREATION,NM_PROGAM_CREATE)
	SELECT @DS_MESSAGE, @XML_REFERENCE, @DT_AFFECTED, @KY_USER, @NM_PROGRAM;

END TRY
BEGIN CATCH  
	ROLLBACK;  
END CATCH; 
	
END