-- =============================================
-- Proyect: Plaskolite
-- Copyright (c) - Acrux - 2017
-- Author: Julio Díaz
-- CREATE date: 02/02/2018
-- Description: Delete a document-role relationship
-- =============================================
CREATE PROCEDURE    [ADM].[SPE_DELETE_DOCUMENT_MANAGEMENT_ROLES]
	@XML_RESULT		  XML = '' OUT  , 
	@PIN_XML_DOCUMENT_ROLES AS XML, 
	@PIN_KY_USER_APP AS NVARCHAR(50), 
	@PIN_NM_PROGRAM AS NVARCHAR(50) 
AS   
BEGIN
	--WE DECLARE THE STARTED VARIABLE THAT INDICATES IF WE WILL HAVE A TRANSACTION ON SPE
	DECLARE @V_EXIST_TRAN BIT = 0
	BEGIN TRY		   			
		--WE VERIFY THAT EXISTS A WORKING TRANSACTION
		IF (@@TRANCOUNT = 0) 
		BEGIN
			--IN CASE THAT THE TRANSACTION DOESNT START
			BEGIN TRANSACTION
			--IT EDITS THE VARIABLE THAT INDICATES THAT THE TRANSACTION START IN THIS BLOCK TO CANCEL IN ANY MOMENT
			SET @V_EXIST_TRAN = 1
		END	

		DECLARE @T_ROLES TABLE (
			ID_DOCUMENT_ROLE INT
		)

		INSERT INTO @T_ROLES (ID_DOCUMENT_ROLE)
		SELECT c.value('@ID_DOCUMENT_ROLE','INT')
		FROM @PIN_XML_DOCUMENT_ROLES.nodes('/ROLES/ROLE') AS T(c)

		DELETE CDR FROM ADM.C_DOCUMENT_ROLE CDR WHERE EXISTS (SELECT TOP 1 1 FROM @T_ROLES TR WHERE TR.ID_DOCUMENT_ROLE = CDR.ID_DOCUMENT_ROLE)

		--WE RETURN A VARIABLE THAT INDICATES THAT EVERYTHING WAS PERFORMED OKAY.
		SET @XML_RESULT = DBO.F_ERROR_CREATE_HEADER( @@ROWCOUNT, 1, 'SUCCESSFUL')
		SET @XML_RESULT = DBO.F_ERROR_INSERT_MESSAGES(@XML_RESULT, 'Se eliminó el item satisfactoriamente', 'ES')
		SET @XML_RESULT = DBO.F_ERROR_INSERT_MESSAGES(@XML_RESULT, 'Successfully roles deleted', 'EN')
		
		--IN THIS BLOCK ALL TRANSACTIONS WILL DELETED
		IF (@@TRANCOUNT > 0 AND @V_EXIST_TRAN = 1)
			COMMIT				
	END TRY
	BEGIN CATCH			
		--IF IT OCCURS A ERROR IN THIS BLOCK THE TRANSACTIO GET CANCELED
		IF (@@TRANCOUNT > 0 AND @V_EXIST_TRAN = 1)
			ROLLBACK
					SET @XML_RESULT = DBO.F_ERROR_MESSAGES(ERROR_NUMBER(), ERROR_MESSAGE())

	END CATCH	
END

