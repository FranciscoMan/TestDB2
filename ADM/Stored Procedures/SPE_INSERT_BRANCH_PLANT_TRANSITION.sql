-- =============================================
-- Proyecto: Plaskolite
-- Copyright (c) - Acrux - 2017
-- Author: Julio Díaz
-- CRETAE date: 21/05/2018
-- Description: Execute the insert or update process to branch plant-transition relationship
-- =============================================
CREATE PROCEDURE    [ADM].[SPE_INSERT_BRANCH_PLANT_TRANSITION] 
		  @XML_RESULT XML OUT
    	, @PIN_XML_BRANCH_PLANT_TRANSITIONS AS XML
		, @PIN_KY_USER AS NVARCHAR(50)
		, @PIN_NM_PROGRAM AS NVARCHAR(50)
AS 
BEGIN  

	DECLARE @V_EXIST_TRAN BIT = 0
		,@DT_SYSTEM DATETIME = GETDATE()

    BEGIN TRY

		IF (@@TRANCOUNT = 0) BEGIN
			BEGIN TRANSACTION

			SET @V_EXIST_TRAN = 1
		END

		DECLARE @T_BRANCH_PLANT_TRANSITIONS TABLE (
			ID_TRANSITION INT
			, ID_BRANCH_PLANT INT
		)

		INSERT INTO @T_BRANCH_PLANT_TRANSITIONS (ID_BRANCH_PLANT, ID_TRANSITION)
		SELECT c.value('@ID_BRANCH_PLANT', 'INT')
			, c.value('@ID_TRANSITION', 'INT')
		FROM @PIN_XML_BRANCH_PLANT_TRANSITIONS.nodes('TRANSITIONS/TRANSITION') t(c)



		INSERT INTO [ADM].[C_BRANCH_PLANT_TRANSITION]
				   ([ID_BRANCH_PLANT]
				   ,[ID_TRANSITION]
				   ,[DT_CREATION]
				   ,[KY_USER_APP_CREATION]
				   ,[NM_PROGRAM_CREATE])
		SELECT T1.ID_BRANCH_PLANT
			, T1.ID_TRANSITION
			, @DT_SYSTEM
			, @PIN_KY_USER
			, @PIN_NM_PROGRAM
		FROM @T_BRANCH_PLANT_TRANSITIONS T1
		WHERE NOT EXISTS (SELECT TOP 1 1 FROM ADM.C_BRANCH_PLANT_TRANSITION T2 WHERE T1.ID_BRANCH_PLANT = T2.ID_BRANCH_PLANT AND T1.ID_TRANSITION = T2.ID_TRANSITION)


		SET @XML_RESULT = DBO.F_ERROR_CREATE_HEADER( @@ROWCOUNT, 1, 'SUCCESSFUL')
		SET @XML_RESULT = DBO.F_ERROR_INSERT_MESSAGES(@XML_RESULT, 'Proceso exitoso', 'ES')
		SET @XML_RESULT = DBO.F_ERROR_INSERT_MESSAGES(@XML_RESULT, 'Successful Process', 'EN')

		IF (@@TRANCOUNT > 0 AND @V_EXIST_TRAN = 1)
			COMMIT	
	END TRY
	BEGIN CATCH		
		--IF IT OCCURS A ERROR IN THIS BLOCK THE TRANSACTIO GET CANCELED
		IF (@@TRANCOUNT > 0 AND @V_EXIST_TRAN = 1)
			ROLLBACK
			
		SET @XML_RESULT = DBO.F_ERROR_MESSAGES(ERROR_NUMBER(), ERROR_MESSAGE())
	END CATCH
END

