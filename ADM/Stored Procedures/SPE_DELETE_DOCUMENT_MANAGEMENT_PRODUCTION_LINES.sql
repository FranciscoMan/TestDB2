-- =============================================
-- Proyect: Plaskolite
-- Copyright (c) - Acrux - 2017
-- Author: Julio Díaz
-- CREATE date: 02/02/2018
-- Description: Delete a document-role relationship
-- =============================================
CREATE PROCEDURE    [ADM].[SPE_DELETE_DOCUMENT_MANAGEMENT_PRODUCTION_LINES]
	@XML_RESULT		  XML = '' OUT  , 
	@PIN_XML_DOCUMENT_PRODUCTION_LINES AS XML, 
	@PIN_KY_USER_APP AS NVARCHAR(50), 
	@PIN_NM_PROGRAM AS NVARCHAR(50) 
AS   
BEGIN
	--WE DECLARE THE STARTED VARIABLE THAT INDICATES IF WE WILL HAVE A TRANSACTION ON SPE
	DECLARE @V_EXIST_TRAN BIT = 0
	BEGIN TRY		   			
		--WE VERIFY THAT EXISTS A WORKING TRANSACTION
		IF (@@TRANCOUNT = 0) 
		BEGIN
			--IN CASE THAT THE TRANSACTION DOESNT START
			BEGIN TRANSACTION
			--IT EDITS THE VARIABLE THAT INDICATES THAT THE TRANSACTION START IN THIS BLOCK TO CANCEL IN ANY MOMENT
			SET @V_EXIST_TRAN = 1
		END	

		DECLARE @T_PRODUCTION_LINES TABLE (
			ID_DOCUMENT_PRODUCTION_LINE INT
		)

		INSERT INTO @T_PRODUCTION_LINES (ID_DOCUMENT_PRODUCTION_LINE)
		SELECT c.value('@ID_DOCUMENT_PRODUCTION_LINE','INT')
		FROM @PIN_XML_DOCUMENT_PRODUCTION_LINES.nodes('/PRODUCTION_LINES/PRODUCTION_LINE') AS T(c)

		DELETE CDPL FROM ADM.C_DOCUMENT_PRODUCTION_LINE CDPL WHERE EXISTS (SELECT TOP 1 1 FROM @T_PRODUCTION_LINES TPL WHERE TPL.ID_DOCUMENT_PRODUCTION_LINE = CDPL.ID_DOCUMENT_PRODUCTION_LINE)

		--WE RETURN A VARIABLE THAT INDICATES THAT EVERYTHING WAS PERFORMED OKAY.
		SET @XML_RESULT = DBO.F_ERROR_CREATE_HEADER( @@ROWCOUNT, 1, 'SUCCESSFUL')
		SET @XML_RESULT = DBO.F_ERROR_INSERT_MESSAGES(@XML_RESULT, 'Se eliminó el item satisfactoriamente', 'ES')
		SET @XML_RESULT = DBO.F_ERROR_INSERT_MESSAGES(@XML_RESULT, 'Successfully production lines deleted', 'EN')
		
		--IN THIS BLOCK ALL TRANSACTIONS WILL DELETED
		IF (@@TRANCOUNT > 0 AND @V_EXIST_TRAN = 1)
			COMMIT				
	END TRY
	BEGIN CATCH			
		--IF IT OCCURS A ERROR IN THIS BLOCK THE TRANSACTIO GET CANCELED
		IF (@@TRANCOUNT > 0 AND @V_EXIST_TRAN = 1)
			ROLLBACK
					SET @XML_RESULT = DBO.F_ERROR_MESSAGES(ERROR_NUMBER(), ERROR_MESSAGE())

	END CATCH	
END

