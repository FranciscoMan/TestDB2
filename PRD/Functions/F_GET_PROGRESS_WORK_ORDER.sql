-- =============================================
-- Author:		Julio Tavares
-- Create date: 12/06/2018
-- Description:	Get the progress work order
-- =============================================
-- 01/29/2019 JDR The variable @NO_SKIDS_ON_HOLD is added to post the skids with status 'on hold'
-- =============================================


CREATE FUNCTION [PRD].[F_GET_PROGRESS_WORK_ORDER] 
(@PIN_ID_WORK_ORDER INT)
RETURNS NVARCHAR(MAX)
AS
BEGIN
	DECLARE @DS_PROGRESS_WORK_ORDER NVARCHAR(MAX) = NULL
		, @NO_RUNNING_SKIDS NVARCHAR(20)
		, @NO_SAVED_SKIDS INT
		, @NO_SKIDS_ON_LIMBO INT
		, @NO_SKIDS_ON_HOLD INT
		, @NO_QTY_SKIDS INT = (SELECT TOP 1 CEILING((NO_RUN_QTY + NO_QTY_ADDED) / NO_QTY_SKID) FROM PRD.K_WORK_ORDER WHERE ID_WORK_ORDER = @PIN_ID_WORK_ORDER)

	DECLARE @T_SKID TABLE (
		NO_SKID INT
		, FG_SAVED_SKID INT
		, FG_RUNNING_SKID INT
		, FG_SKID_ON_LIMBO INT
		, FG_SKID_ON_HOLD INT
	)
	INSERT INTO @T_SKID(
		NO_SKID
		, FG_SAVED_SKID
		, FG_RUNNING_SKID
		, FG_SKID_ON_LIMBO
		, FG_SKID_ON_HOLD
	)
	SELECT NO_PALLET
		, CASE WHEN VCPS.FG_FOR_SAVE = 1 THEN 1 ELSE 0 END AS FG_SAVED_SKID
		, CASE WHEN KP.FG_SEND_FORM = 1 THEN 1 ELSE 0 END AS FG_RUNNING_SKID
		, CASE WHEN VCPS.KY_TEMP_STATUS = 'L' THEN 1 ELSE 0 END AS FG_SKID_ON_LIMBO
		, CASE WHEN VCPS.KY_TEMP_STATUS = 'H' THEN 1 ELSE 0 END AS FG_SKID_ON_HOLD
	FROM PRD.K_PALLET KP
		INNER JOIN ADM.VW_C_PALLET_STATUS VCPS
			ON KP.KY_STATUS = VCPS.KY_PALLET_STATUS
	WHERE ID_WORK_ORDER = @PIN_ID_WORK_ORDER
		
	SELECT @NO_SAVED_SKIDS = SUM(FG_SAVED_SKID)
		, @NO_SKIDS_ON_LIMBO = SUM(FG_SKID_ON_LIMBO)
		, @NO_SKIDS_ON_HOLD = SUM(FG_SKID_ON_HOLD)
	FROM @T_SKID

	SELECT @NO_RUNNING_SKIDS = COALESCE(@NO_RUNNING_SKIDS + ', ' + CONVERT(NVARCHAR(5), NO_SKID), CONVERT(NVARCHAR(5), NO_SKID)) FROM @T_SKID WHERE FG_RUNNING_SKID = 1

	SET @DS_PROGRESS_WORK_ORDER = 'Working on skid(s) No. ' + ISNULL(@NO_RUNNING_SKIDS, '-') + '. Saved ' + CAST(ISNULL(@NO_SAVED_SKIDS, 0) AS NVARCHAR) + ' of ' + CAST(ISNULL(@NO_QTY_SKIDS, 0) AS NVARCHAR) + ' skids. No. skids on hold: ' +CAST(ISNULL(@NO_SKIDS_ON_HOLD, 0) AS NVARCHAR)

	RETURN @DS_PROGRESS_WORK_ORDER
END