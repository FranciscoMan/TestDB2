-- =============================================
-- Author:		Gabriel Vázquez Torres
-- Create date: 29/06/2018
-- Description:	Get the process forms by date
-- =============================================
-- 11/14/2018 JDR Se agregan las columnas KY_USER_AUTHORIZED_CANCEL, NM_USER_AUTHORIZED_CANCEL, DS_EXPLANATION_CANCEL, KY_USER_AUTHORIZED_OUT_OF_RANGE, NM_USER_AUTHORIZED_OUT_OF_RANGE, DS_EXPLANATION_OUT_OF_RANGE
-- =============================================

CREATE PROCEDURE    [PRD].[SPE_GET_PROCESS_FORM_REPORT]
	@PIN_ID_WORK_ORDER AS INT = NULL
	, @PIN_ID_BRANCH_PLANT AS INT = NULL
	, @PIN_ID_PRODUCTION_LINE AS INT = NULL
	, @PIN_DT_INITIAL AS DATE = NULL
	, @PIN_DT_END AS DATE = NULL
AS
BEGIN
	-- SET NOCOUNT ON added to prevent extra result sets from
	-- interfering with SELECT statements.
	SET NOCOUNT ON;

	SELECT KF.ID_K_FORM
		, KF.ID_PRODUCTION_LINE
		, KF.DT_FORM
		, KF.DT_CLOSED
		, KF.KY_STATUS_FORM
		, KWO.ID_WORK_ORDER
		, KWO.ID_ITEM
		, CI.KY_ITEM
		, KWO.NM_ITEM
		, CBP.ID_CUT_TIME_METRIC
		, CBP.ID_TOTAL_CUT_WEIGHT_METRIC
		, CBP.ID_LBS_PER_HOUR_METRIC
		, CBP.ID_PARTS_EXT_METRIC
		, ISNULL((SELECT XML_METRICS_VALUE.value('(/FIELD_TYPES/@FINISHED_VALUE)[1]', 'nvarchar(50)') AS FINISSHED_VALUE FROM PRD.K_FORM_METRICS KFM WHERE KFM.ID_K_FORM = KF.ID_K_FORM AND KFM.ID_METRICS = CBP.ID_CUT_TIME_METRIC),'') AS NO_CUT_TIME_METRIC
		, ISNULL((SELECT XML_METRICS_VALUE.value('(/FIELD_TYPES/@FINISHED_VALUE)[1]', 'nvarchar(50)') AS FINISSHED_VALUE FROM PRD.K_FORM_METRICS KFM WHERE KFM.ID_K_FORM = KF.ID_K_FORM AND KFM.ID_METRICS = CBP.ID_TOTAL_CUT_WEIGHT_METRIC),'') AS NO_TOTAL_CUT_WEIGHT_METRIC
		, ISNULL((SELECT XML_METRICS_VALUE.value('(/FIELD_TYPES/@FINISHED_VALUE)[1]', 'nvarchar(50)') AS FINISSHED_VALUE FROM PRD.K_FORM_METRICS KFM WHERE KFM.ID_K_FORM = KF.ID_K_FORM AND KFM.ID_METRICS = CBP.ID_LBS_PER_HOUR_METRIC),'') AS NO_LBS_PER_HOUR_METRIC
		, ISNULL((SELECT XML_METRICS_VALUE.value('(/FIELD_TYPES/@FINISHED_VALUE)[1]', 'nvarchar(50)') AS FINISSHED_VALUE FROM PRD.K_FORM_METRICS KFM WHERE KFM.ID_K_FORM = KF.ID_K_FORM AND KFM.ID_METRICS = CBP.ID_PARTS_EXT_METRIC),'') AS NO_PARTS_EXT_METRIC
		, KF.KY_USER_AUTHORIZED_CANCEL
		, KF.NM_USER_AUTHORIZED_CANCEL
		, KF.DS_EXPLANATION_CANCEL
		, KF.KY_USER_AUTHORIZED_OUT_OF_RANGE
		, KF.NM_USER_AUTHORIZED_OUT_OF_RANGE
		, KF.DS_EXPLANATION_OUT_OF_RANGE
	FROM PRD.K_FORM KF
		INNER JOIN PRD.K_WORK_ORDER KWO 
			ON KF.ID_WORK_ORDER = KWO.ID_WORK_ORDER
		INNER JOIN PRD.C_ITEM CI 
			ON KWO.ID_ITEM = CI.ID_ITEM
		INNER JOIN ADM.C_BRANCH_PLANT CBP 
			ON KF.ID_BRANCH_PLANT = CBP.ID_BRANCH_PLANT
	WHERE KF.KY_PROCESS_TYPE = 'PROCESS' AND 
		(@PIN_DT_INITIAL IS NULL OR (@PIN_DT_INITIAL IS NOT NULL AND KF.DT_FORM BETWEEN @PIN_DT_INITIAL AND @PIN_DT_END)) AND
		(@PIN_ID_WORK_ORDER IS NULL OR (@PIN_ID_WORK_ORDER IS NOT NULL AND KF.ID_WORK_ORDER = @PIN_ID_WORK_ORDER)) AND
		(@PIN_ID_BRANCH_PLANT IS NULL OR (@PIN_ID_BRANCH_PLANT IS NOT NULL AND KF.ID_BRANCH_PLANT = @PIN_ID_BRANCH_PLANT)) AND
		(@PIN_ID_PRODUCTION_LINE IS NULL OR (@PIN_ID_PRODUCTION_LINE IS NOT NULL AND KF.ID_PRODUCTION_LINE = @PIN_ID_PRODUCTION_LINE))
	ORDER BY KF.DT_CREATION DESC

END

