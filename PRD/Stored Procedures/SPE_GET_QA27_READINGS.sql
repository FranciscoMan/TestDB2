-- =============================================
-- Proyecto: Plaskolite
-- Copyright (c) - Acrux - 2018
-- Author: Julio Díaz
-- CREATE date: 10/25/2018
-- Description: Get readings for a QA27 record
-- =============================================
-- Line opt was added. by AA

CREATE PROCEDURE  [PRD].[SPE_GET_QA27_READINGS]
	@PIN_ID_QA27 AS INT = NULL
AS   
BEGIN		

	DECLARE @ID_METRIC_LENGTH INT
		, @ID_METRIC_WIDTH INT
		, @ID_METRIC_WEIGHT INT
		, @ID_METRIC_THICKNESS INT
		, @ID_METRIC_GLOSS INT
		, @ID_METRIC_LIGHT_TRANSMISSION INT
		, @SWITCH_FLAG BIT 

	SELECT TOP 1 @ID_METRIC_LENGTH = CBP.ID_LENGTH_METRIC
		, @ID_METRIC_WIDTH = CBP.ID_WIDTH_METRIC
		, @ID_METRIC_WEIGHT = CBP.ID_WEIGHT_METRIC
		, @ID_METRIC_THICKNESS = CBP.ID_THICKNESS_METRIC
		, @ID_METRIC_GLOSS = CBP.ID_GLOSS_METRIC
		, @ID_METRIC_LIGHT_TRANSMISSION = CBP.ID_LIGHT_TRANSMISSION_METRIC
	FROM ADM.C_BRANCH_PLANT CBP
	WHERE EXISTS (SELECT TOP 1 1 FROM PRD.K_WORK_ORDER KWO WHERE KWO.ID_BRANCH_PLANT = CBP.ID_BRANCH_PLANT AND 
	     EXISTS (SELECT TOP 1 1 FROM PRD.K_QA27 KQ WHERE KQ.ID_WORK_ORDER = KWO.ID_WORK_ORDER AND KQ.ID_QA27 = @PIN_ID_QA27))

	BEGIN
		SELECT KF.ID_K_FORM
		, CM.ID_METRICS
		, CM.NM_METRICS
		, KFM.XML_METRICS_VALUE
		, CM.XML_FIELD_SETTINGS
		, KF.DT_FORM
		, KF.DT_CLOSED
		, KP.NO_PALLET
		, KF.KY_PROCESS_TYPE
		, CM.NO_ORDER
		, KFM.KY_USER_APP_UPDATE
		, CASE WHEN CM.ID_METRICS IN (@ID_METRIC_LENGTH, @ID_METRIC_WIDTH, @ID_METRIC_WEIGHT, @ID_METRIC_THICKNESS, 
		           @ID_METRIC_GLOSS, @ID_METRIC_LIGHT_TRANSMISSION) THEN CAST(1 AS BIT) ELSE CAST(0 AS BIT) END AS FG_ACQUISITION_BY_DEVICE
	    , KFM.NM_PROGAM_CREATE
	FROM PRD.K_FORM KF
		INNER JOIN PRD.K_FORM_METRICS KFM
			ON KF.ID_K_FORM = KFM.ID_K_FORM
			AND KF.ID_QA27 = @PIN_ID_QA27
			AND KF.KY_PROCESS_TYPE IN ('PROCESS', 'MANUFACTURE', 'QUALITY', 'LINE_OPT')
			AND KF.KY_STATUS_FORM = 'CAPTURED'
		INNER JOIN PRD.C_METRICS CM
			ON CM.ID_METRICS = KFM.ID_METRICS
		LEFT JOIN PRD.C_FORM_METRICS CFM
			ON CFM.ID_METRICS = CM.ID_METRICS
			AND CFM.ID_FORM = KF.ID_FORM
		LEFT JOIN PRD.K_PALLET KP
			ON KF.ID_PALLET = KP.ID_PALLET		
	ORDER BY KF.KY_PROCESS_TYPE, KF.DT_FORM, CM.NO_ORDER
	END

END

