-- =============================================
-- Author:		Gabriel Vázquez Torres
-- Create date: 26/05/2018
-- Description:	Insert or update the ip production line
-- =============================================
CREATE PROCEDURE    [PRD].[SPE_INSERT_UPDATE_PRODUCTION_LINE_IP]
	-- Add the parameters for the stored procedure here
	  @XML_RESULT XML = '' OUT    -- --0 TO ERROR AND 1 TO CORRECT
	, @PIN_ID_PRODUCTION_LINE_IP AS INT
	, @PIN_ID_PRODUCTION_LINE AS INT
	, @PIN_NO_IP AS nvarchar(40)
	, @PIN_KY_USER_APP NVARCHAR(50) 
	, @PIN_NM_PROGAM NVARCHAR(50) 
	, @PIN_TYPE_TRANSACTION CHAR(1)  --I=INSERT   U=UPDATE
AS
BEGIN
	DECLARE @V_EXIST_TRAN BIT = 0
		,@DT_SYSTEM DATETIME = GETDATE()
		,@V_KY_ITEM INT
   	BEGIN TRY
		--WE VERIFY THAT EXISTS A WORKING TRANSACTION
		IF (@@TRANCOUNT = 0) BEGIN
			--IN CASE THAT THE TRANSACTION DOESNT START
			BEGIN TRANSACTION
			--IT EDITS THE VARIABLE THAT INDICATES THAT THE TRANSACTION START IN THIS BLOCK TO CANCEL IN ANY MOMENT
			SET @V_EXIST_TRAN = 1
		END	

		IF @PIN_TYPE_TRANSACTION = 'I' BEGIN

			INSERT INTO [PRD].[C_PRODUCTION_LINE_IP]
					   ([ID_PRODUCTION_LINE]
					   ,[NO_IP]
					   ,[DT_CREATION]
					   ,[KY_USER_APP_CREATION]
					   ,[NM_PROGRAM_CREATE])
				 VALUES
					   ( @PIN_ID_PRODUCTION_LINE
						,@PIN_NO_IP
						,@DT_SYSTEM
						,@PIN_KY_USER_APP
					    ,@PIN_NM_PROGAM)
		END
		ELSE BEGIN

			UPDATE [PRD].[C_PRODUCTION_LINE_IP]
			   SET 
				   [NO_IP] = @PIN_NO_IP
				  ,[DT_UPDATE] = @DT_SYSTEM
				  ,[KY_USER_APP_UPDATE] = @PIN_KY_USER_APP
				  ,[NM_PROGRAM_UPDATE] = @PIN_NM_PROGAM
			 WHERE ID_PRODUCTION_LINE_IP = @PIN_ID_PRODUCTION_LINE_IP

		END

 		-- WE BACK A RETURN VARIABLE THAT INDICATES ALL WAS PERFORMED OKAY 
		SET @XML_RESULT = DBO.F_ERROR_CREATE_HEADER( @@ROWCOUNT, 1, 'SUCCESSFUL')
		SET @XML_RESULT = DBO.F_ERROR_INSERT_MESSAGES(@XML_RESULT, 'Proceso exitoso', 'ES')
		SET @XML_RESULT = DBO.F_ERROR_INSERT_MESSAGES(@XML_RESULT, 'Successful Process', 'EN')
		-- IF THERE IS A TRANSACTION IN THIS BLOCK, IT WILL BE ERASED
		
		IF (@@TRANCOUNT > 0 AND @V_EXIST_TRAN = 1)
			COMMIT	
	END TRY
	BEGIN CATCH		
		--IF IT OCCURS A ERROR IN THIS BLOCK THE TRANSACTIO GET CANCELED
		IF (@@TRANCOUNT > 0 AND @V_EXIST_TRAN = 1)
			ROLLBACK
			
	    SET @XML_RESULT = DBO.F_ERROR_MESSAGES(ERROR_NUMBER(), ERROR_MESSAGE())
			
	END CATCH


END

