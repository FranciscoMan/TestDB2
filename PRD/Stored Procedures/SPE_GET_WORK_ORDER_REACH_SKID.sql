-- =============================================
-- Proyect: Plaskolite
-- Copyright (c) - Acrux - 2017
-- Author: Julio César Tavares
-- CREATE date: 01/20/219
-- Description: Get running work order information for reach skid process
-- =============================================

CREATE PROCEDURE    [PRD].[SPE_GET_WORK_ORDER_REACH_SKID] 
	@PIN_ID_WORK_ORDER INT
AS
BEGIN
	DECLARE @ID_QA27 INT
		, @DT_QA27_INITIAL_TIME DATETIME
		, @NO_LAST_SKID INT
		, @NO_RUN_QTY INT
		, @NO_QTY_ADDED INT
		, @NO_QTY_SKID INT
		, @NO_REQUIRED_SKIDS INT
		, @NO_REMAINING_SKIDS INT
		, @XML_SKIDS_TO_SELECT XML

		, @NO_SAVED_SKIDS INT = (
			SELECT COUNT(*)
			FROM PRD.K_PALLET KP
				INNER JOIN ADM.VW_C_PALLET_STATUS VCPS
					ON KP.KY_STATUS = VCPS.KY_PALLET_STATUS
					AND VCPS.FG_FOR_SAVE = 1
			WHERE ID_WORK_ORDER = @PIN_ID_WORK_ORDER
		)

	SELECT TOP 1 @ID_QA27 = KQ.ID_QA27
		, @DT_QA27_INITIAL_TIME = KQ.DT_INITIAL_TIME
		, @NO_LAST_SKID = NO_PALLET
	FROM PRD.K_PALLET KP
		INNER JOIN PRD.K_QA27 KQ
			ON KP.ID_QA27 = KQ.ID_QA27
	WHERE KP.ID_WORK_ORDER = @PIN_ID_WORK_ORDER
	ORDER BY KP.DT_INITIAL_TIME DESC, KP.NO_PALLET DESC

	CREATE TABLE #T_SKIDS  (
		NO_SKID INT
		, KY_STATUS NVARCHAR(20)
	)

	SELECT TOP 1 @NO_RUN_QTY = NO_RUN_QTY
		, @NO_QTY_ADDED = NO_QTY_ADDED
		, @NO_QTY_SKID = NO_QTY_SKID
		, @NO_REQUIRED_SKIDS = CEILING((NO_RUN_QTY + NO_QTY_ADDED) / NO_QTY_SKID)
		, @NO_REMAINING_SKIDS = CEILING((NO_RUN_QTY + NO_QTY_ADDED) / NO_QTY_SKID) - @NO_SAVED_SKIDS
	FROM PRD.K_WORK_ORDER
	WHERE ID_WORK_ORDER = @PIN_ID_WORK_ORDER


	; WITH T_SKID AS (
		SELECT @NO_LAST_SKID + 1 AS NO_SKID UNION ALL
		SELECT NO_SKID + 1 FROM T_SKID WHERE NO_SKID < @NO_LAST_SKID + @NO_REMAINING_SKIDS - 1
	)
	INSERT INTO #T_SKIDS (NO_SKID)
	SELECT NO_SKID FROM T_SKID
	option (maxrecursion 0)

	SET @XML_SKIDS_TO_SELECT = (
			SELECT NO_SKID AS '@NO_SKID'
			FROM #T_SKIDS
			FOR XML PATH ('SKID'), ROOT('SKIDS')
		)

	SELECT @NO_RUN_QTY AS NO_RUN_QTY
		, @NO_QTY_ADDED AS NO_QTY_ADDED
		, @NO_QTY_SKID AS nO_QTY_SKID
		, @NO_REQUIRED_SKIDS AS NO_REQUIRED_SKIDS
		, @NO_SAVED_SKIDS AS NO_SAVED_SKIDS
		, @NO_REMAINING_SKIDS AS NO_REMAINING_SKIDS
		, @NO_LAST_SKID AS NO_LAST_SKID
		, @ID_QA27 AS ID_LAST_QA27_WITH_SKIDS
		, @DT_QA27_INITIAL_TIME AS DT_LAST_QA27_INITIAL_TIME
		, @XML_SKIDS_TO_SELECT AS XML_SKIDS_TO_SELECT

END

