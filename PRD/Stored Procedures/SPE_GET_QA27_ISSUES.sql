
-- ====================================== =======
-- Proyecto: Plaskolite
-- Copyright (c) - Acrux - 2018
-- Author: Julio Tavares
-- CREATE date: 3/08/2018
-- Description: get all issues qa27
-- =============================================
-- 12/11/2018 JDR The query is rewritten
-- =============================================
CREATE PROCEDURE   [PRD].[SPE_GET_QA27_ISSUES] 
	@PIN_ID_QA27 AS INT = NULL
AS   
	
	DECLARE @DT_SHIFT_BEGIN DATETIME
		, @DT_SHIFT_END DATETIME
		, @DT_QA27_BEGIN DATETIME
		, @DT_QA27_END DATETIME
		, @ID_WORK_ORDER INT
		, @NO_SHIFT_HOURS INT
		, @DT_SYSTEM DATETIME = GETDATE()

	SELECT @DT_SHIFT_BEGIN = KS.DT_START_SHIFT
		, @DT_SHIFT_END = KS.DT_END_SHIFT
		, @DT_QA27_BEGIN = KQ.DT_INITIAL_TIME
		, @DT_QA27_END = KQ.DT_FINAL_TIME
		, @ID_WORK_ORDER = KQ.ID_WORK_ORDER
		, @NO_SHIFT_HOURS = DATEDIFF(HOUR, @DT_SHIFT_BEGIN, @DT_SHIFT_END)
	FROM PRD.K_QA27 KQ
		INNER JOIN PRD.K_SHIFT KS
			ON KQ.ID_SHIFT = KS.ID_SHIFT
	WHERE KQ.ID_QA27 = @PIN_ID_QA27

	SELECT KI.ID_ISSUE
		, CASE WHEN KI.DT_ISSUE < @DT_QA27_BEGIN THEN @DT_QA27_BEGIN ELSE KI.DT_ISSUE END AS DT_ISSUE_BEGIN
		, CASE WHEN KI.DT_ISSUE_CLOSED IS NULL OR KI.KY_STATUS ='HOLD_ON' THEN NULL  
			   WHEN KI.DT_ISSUE_CLOSED > @DT_QA27_END THEN @DT_QA27_END ELSE KI.DT_ISSUE_CLOSED END AS DT_ISSUE_END
		, KI.FG_LINE_DOWN
		, KI.KY_STATUS
		, CPC.KY_CODE_TYPE
		, CPC.KY_PROBLEM_CODE
		, CPC.NM_PROBLEM_CODE
		, CPA.NM_PROBLEM_AREA
		, KI.DS_EXPLANATION_EVENT_CLOSED
		, KI.DS_ISSUE_EXPLANATION_OPEN
		, @DT_QA27_BEGIN AS DT_QA27_BEGIN
		, @DT_QA27_END AS DT_QA27_END
		, @DT_SHIFT_BEGIN AS DT_SHIFT_BEGIN
		, @DT_SHIFT_END AS DT_SHIFT_END
	FROM PRD.K_ISSUE KI
		INNER JOIN PRD.C_PROBLEM_CODE CPC
			ON KI.ID_PROBLEM_CODE = CPC.ID_PROBLEM_CODE
		INNER JOIN PRD.C_PROBLEM_AREA CPA
			ON KI.ID_PROBLEM_AREA = CPA.ID_PROBLEM_AREA
	WHERE KI.ID_WORK_ORDER = @ID_WORK_ORDER
		AND (KI.DT_ISSUE BETWEEN @DT_QA27_BEGIN AND ISNULL(@DT_QA27_END, @DT_SYSTEM)
			OR KI.DT_ISSUE_CLOSED BETWEEN @DT_QA27_BEGIN AND ISNULL(@DT_QA27_END, @DT_SYSTEM)
			OR @DT_QA27_BEGIN BETWEEN KI.DT_ISSUE AND ISNULL(KI.DT_ISSUE_CLOSED, @DT_SYSTEM)
		) ORDER BY KI.ID_ISSUE DESC
