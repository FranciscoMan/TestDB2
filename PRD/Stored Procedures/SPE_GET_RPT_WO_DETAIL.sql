-- =============================================
-- Proyecto: Plaskolite
-- Copyright (c) - Acrux - 2017
-- Author: Javier Diaz Barron
-- CREATE date: 29/05/2017
-- Description: Report Work Order Detail 
-- =============================================
CREATE PROCEDURE    [PRD].[SPE_GET_RPT_WO_DETAIL]
	@ACCION NVARCHAR(50)
   ,@NO_WORK_ORDER INT 		
   , @DT_WORK_ORDER DATE
   , @KY_SHIFT NVARCHAR(20)
AS   
BEGIN

	DECLARE @STATUS_QA27 NVARCHAR(50) = 'RUNNING'

	IF @ACCION  = 'HEAD' BEGIN
		SELECT
			 WO.NM_CUSTOMER
			, WO.NO_WORK_ORDER
			, CI.KY_ITEM
			, CI.DS_ITEM
			, WO.NM_PRODUCTION_LINE
			, WO.DT_WORK_ORDER
			, CAST(WO.NO_RUN_QTY AS NVARCHAR(10)) + ' pcs.' NO_RUN_QTY
			, CAST(WO.NO_QTY_ADDED AS NVARCHAR(10)) + ' pcs.' NO_QTY_ADDED
			, CAST(WO.NO_QTY_SKID AS NVARCHAR(10)) + ' Pcs/skid' NO_QTY_SKID
			, WO.NM_MATERIAL
			, WO.NM_COLOR
			, CAST(WO.NO_LENGHT AS NVARCHAR(10)) + ' in.' NO_LENGHT
			, CAST(WO.NO_WIDTH AS NVARCHAR(10)) + ' in.' NO_WIDTH
			, CAST(WO.NO_THICKNESS AS NVARCHAR(10)) + ' in.' NO_THICKNESS
		FROM PRD.K_WORK_ORDER WO
			INNER JOIN PRD.C_ITEM CI 
				ON CI.ID_ITEM = WO.ID_ITEM 
		WHERE WO.NO_WORK_ORDER = @NO_WORK_ORDER

	END
	
	
	IF @ACCION = 'ISSUES' BEGIN
		
		;WITH TB_LOST_TIMES AS 
		(
			SELECT 
				 KI.ID_ISSUE
				,PA.NM_PROBLEM_AREA
				,PC.NM_PROBLEM_CODE
				,'' DEEPER_SYMPTOM
				,CASE WHEN PA.NM_PROBLEM_AREA = 'TRANSITIONS' THEN '' ELSE ISNULL(DFC.NM_DEFECT_CATEGORY,'N/A') END NM_DEFECT_CATEGORY
				,'' ACTION
				,KI.DT_ISSUE DT_START_ISSUE
				,DATEDIFF(MINUTE,KI.DT_ISSUE,ISNULL(KI.DT_CONFIRMED, GETDATE())) / 60.0 DT_USER_INVOLVED_MINUTES
				,DATEDIFF(MINUTE,KI.DT_ISSUE,ISNULL(KI.DT_ISSUE_CLOSED, GETDATE())) / 60.0 DT_FINISH_ISSUE		
			FROM PRD.C_PROBLEM_AREA PA
				INNER JOIN PRD.C_PROBLEM_CODE PC 
					ON PC.ID_PROBLEM_AREA = PA.ID_PROBLEM_AREA
				INNER JOIN PRD.K_ISSUE KI 
					ON KI.ID_PROBLEM_AREA = PA.ID_PROBLEM_AREA 
						AND KI.ID_PROBLEM_CODE = PC.ID_PROBLEM_CODE
				INNER JOIN PRD.K_WORK_ORDER WO 
					ON WO.ID_WORK_ORDER = KI.ID_WORK_ORDER
				LEFT JOIN PRD.C_DEFECT_CATEGORY DFC 
					ON DFC.ID_DEFECT_CATEGORY = KI.ID_DEFECT_CATEGORY			
			WHERE WO.NO_WORK_ORDER	= @NO_WORK_ORDER		
				AND (@DT_WORK_ORDER IS NULL OR (@DT_WORK_ORDER BETWEEN CAST(KI.DT_ISSUE AS DATE) AND CAST(ISNULL(KI.DT_ISSUE_CLOSED, GETDATE()) AS DATE)))
		)
		
		SELECT 
			 NM_PROBLEM_AREA
			,NM_PROBLEM_CODE
			,DEEPER_SYMPTOM
			,NM_DEFECT_CATEGORY
			,ACTION
			,DT_START_ISSUE
			,DT_USER_INVOLVED_MINUTES
			,DT_FINISH_ISSUE
			,(SELECT (DT_USER_INVOLVED_MINUTES + DT_FINISH_ISSUE) FROM TB_LOST_TIMES WHERE ID_ISSUE= LT.ID_ISSUE) TOTAL_TIME
		FROM TB_LOST_TIMES LT		
		
	END

	IF @ACCION = 'PIN_DATES' BEGIN
		; WITH T_DATES AS (
			SELECT CAST(ISNULL(DT_START_WORK_ORDER, GETDATE()) AS DATE) AS DT_START_WO, CAST(ISNULL(DT_CLOSE_WORK_ORDER, GETDATE()) AS DATE) AS DT_CLOSE_WO
			FROM PRD.K_WORK_ORDER
			WHERE ID_WORK_ORDER = @NO_WORK_ORDER
			UNION ALL 
			SELECT DATEADD(DAY, 1, TD.DT_START_WO), DT_CLOSE_WO
			FROM T_DATES TD
			WHERE DT_START_WO < DT_CLOSE_WO
		)

		SELECT DT_START_WO, CONVERT(NVARCHAR(30), DT_START_WO, 107) AS DS_DT_START_WO FROM T_DATES
	END

END

