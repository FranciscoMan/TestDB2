-- =============================================
-- Proyecto: Plaskolite
-- Copyright (c) - Acrux - 2018
-- Author: Julio Tavares
-- CREATE date: 05/05/2018
-- Description: get autorization production line
-- =============================================
-- 21/05/2018 JDR @PIN_DT_INITIAL and @PIN_DT_FINAL parameters added
-- 23/07/2018 JDR KY_SHIFT_TIME column added
-- =============================================
CREATE PROCEDURE    [PRD].[SPE_GET_AUTHORIZATIONS_PRODUCTION_LINE] 
	@PIN_ID_BRANCH_PLANT AS INT = NULL
	, @PIN_ID_PRODUCTION_LINE AS INT = NULL
	, @PIN_DT_INITIAL AS DATE = NULL
	, @PIN_DT_FINAL AS DATE = NULL
AS   
BEGIN

	DECLARE @DT_INITIAL DATE = ISNULL(@PIN_DT_INITIAL, DATEADD(DAY, -7, GETDATE()))
		, @DT_FINAL DATE = ISNULL(@PIN_DT_FINAL, GETDATE())

	SELECT KS.DT_SHIFT_HISTORY AS DT_DATE, 
		--CAST(KS.DT_SHIFT_HISTORY AS TIME) DT_TIME,
		KS.ID_PRODUCTION_LINE, 
		CPL.NM_PRODUCTION_LINE,
		KS.ID_BRANCH_PLANT, 
		CB.NM_BRANCH_PLANT,
		KS.KY_USER, 
		CU.NM_USER,
		KS.KY_AUTHORIZER_USER,
		CASE WHEN KS.KY_AUTHORIZER_USER = 'self authorized' THEN '' ELSE CUA.NM_USER END AS NM_AUTHORIZER_USER
		, REPLACE(KS.KY_SHIFT_TIME, 'SF-', '') AS KY_SHIFT_TIME
	FROM PRD.K_SHIFT KS
		JOIN ADM.C_BRANCH_PLANT CB 
			ON KS.ID_BRANCH_PLANT = CB.ID_BRANCH_PLANT
		JOIN PRD.C_PRODUCTION_LINE CPL 
			ON KS.ID_PRODUCTION_LINE = CPL.ID_PRODUCTION_LINE
		JOIN ADM.C_USER CU 
			ON KS.KY_USER = CU.KY_USER
		LEFT JOIN ADM.C_USER CUA 
			ON KS.KY_AUTHORIZER_USER = CUA.KY_USER
	WHERE (@PIN_ID_BRANCH_PLANT IS NULL OR (@PIN_ID_BRANCH_PLANT IS NOT NULL AND KS.ID_BRANCH_PLANT = @PIN_ID_BRANCH_PLANT))
		AND (@PIN_ID_PRODUCTION_LINE IS NULL OR (@PIN_ID_PRODUCTION_LINE IS NOT NULL AND KS.ID_PRODUCTION_LINE = @PIN_ID_PRODUCTION_LINE))
		AND CAST(KS.DT_SHIFT_HISTORY AS DATE) BETWEEN @DT_INITIAL AND @DT_FINAL
		--AND KS.KY_AUTHORIZER_USER != 'self authorized'
	ORDER BY KS.DT_SHIFT_HISTORY DESC

END

