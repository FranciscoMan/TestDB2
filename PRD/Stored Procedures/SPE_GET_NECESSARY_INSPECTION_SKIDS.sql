-- =============================================
-- Proyecto: Plaskolite
-- Copyright (c) - Acrux - 2017
-- Author: Julio Tavares	
-- CREATE date: 17/07/2018
-- Description: Get all the skids needed to complete the order
-- =============================================
-- 12/08/2018 JDR Query is rewritten
-- =============================================
CREATE PROCEDURE    [PRD].[SPE_GET_NECESSARY_INSPECTION_SKIDS]
     @PIN_ID_WORK_ORDER INT = NULL
AS   
BEGIN

	DECLARE @NO_SKIDS_NEEDED INT
		, @NO_SKIDS_SAVED INT
		, @NO_SKIDS_NOT_SAVED INT
		, @NO_SKIDS_TOTAL INT

	SELECT @NO_SKIDS_SAVED = SUM(CASE WHEN VCPS.FG_FOR_SAVE = 1 OR KP.KY_STATUS = 'WORKING' THEN 1 ELSE 0 END)
		, @NO_SKIDS_NOT_SAVED = SUM(CASE WHEN VCPS.FG_FOR_SAVE = 1 OR KP.KY_STATUS = 'WORKING' THEN 0 ELSE 1 END)
	FROM PRD.K_PALLET KP
		INNER JOIN ADM.VW_C_PALLET_STATUS VCPS 
			ON KP.KY_STATUS = VCPS.KY_PALLET_STATUS 
	WHERE ID_WORK_ORDER = @PIN_ID_WORK_ORDER

	SELECT TOP 1 @NO_SKIDS_NEEDED = CEILING(CAST((KWO.NO_RUN_QTY + KWO.NO_QTY_ADDED) AS DECIMAL) / CAST(KWO.NO_QTY_SKID AS DECIMAL))
	FROM PRD.K_WORK_ORDER KWO
	WHERE KWO.ID_WORK_ORDER = @PIN_ID_WORK_ORDER

	; WITH T_SKIDS AS (
		SELECT 1 AS NO_SKID
		UNION ALL 
		SELECT NO_SKID + 1 FROM T_SKIDS WHERE NO_SKID < (@NO_SKIDS_NEEDED + ISNULL(@NO_SKIDS_NOT_SAVED, 0))
	)
	SELECT KP.ID_PALLET
		, KIS.ID_INSPECTION_SKID
		, TS.NO_SKID
		, ISNULL(KP.KY_STATUS, 'NON_CREATED') AS KY_STATUS_PALLET
		, ISNULL(VCPSO.NM_PALLET_STATUS, 'Non created') AS NM_STATUS_PALLET
		, @PIN_ID_WORK_ORDER AS ID_WORK_ORDER
		, ISNULL(KIS.KY_STATUS, 'NON_CREATED') AS KY_STATUS_INSPECTION_SKID
		, ISNULL(VCPSI.NM_PALLET_STATUS, 'Non created') AS NM_STATUS_INSPECTION_SKID
	FROM T_SKIDS TS
		LEFT JOIN PRD.K_PALLET KP
			ON KP.ID_WORK_ORDER = @PIN_ID_WORK_ORDER
				AND TS.NO_SKID = KP.NO_PALLET
		LEFT JOIN ADM.VW_C_PALLET_STATUS VCPSO
			ON KP.KY_STATUS = VCPSO.KY_PALLET_STATUS
		LEFT JOIN PRD.K_INSPECTION_SKID KIS
			ON KIS.ID_WORK_ORDER = @PIN_ID_WORK_ORDER
				AND TS.NO_SKID = KIS.NO_PALLET
		LEFT JOIN ADM.VW_C_PALLET_STATUS VCPSI
			ON KIS.KY_STATUS = VCPSI.KY_PALLET_STATUS
	ORDER BY TS.NO_SKID
	OPTION (maxrecursion 0);

END

