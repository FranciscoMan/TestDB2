-- =============================================
-- Proyecto: Plaskolite
-- Copyright (c) - Acrux - 2017
-- Author: Julio Tavares
-- CREATE date: 14/07/2018
-- Description: get all form by PROCESS QUALITY
-- =============================================
CREATE PROCEDURE    [PRD].[SPE_GET_FORM_BY_PROCESS] 
	 @KY_PROCESS NVARCHAR(50) 
	,@ID_PALLET INT = NULL
	,@ID_WORK_ORDER INT = NULL 
	,@ID_FORM INT = NULL 
	,@ID_PRODUCTION_LINE INT = NULL 
	,@ID_K_FORM INT = NULL 


AS   

	SELECT CF.ID_FORM
		, CF.KY_FORM
		, CF.NM_FORM
		, CF.NO_FREQUENCE
		, VCP.KY_PROCESS
		, VCP.NM_PROCESS
		, CF.NO_SAMPLE
		, DA.NM_DATA_ACQUISITION_ORIGIN	
		, ISNULL(KF.KY_STATUS_FORM,'NOT_SAVED') KY_STATUS_K_FORM
		, KF.ID_PALLET
		, KF.ID_WORK_ORDER
		, KF.ID_PRODUCTION_LINE
		, KF.ID_K_FORM		
	FROM PRD.C_FORM CF
		INNER JOIN ADM.VW_C_PROCESS VCP 
			ON VCP.KY_PROCESS = CF.KY_PROCESS
			AND VCP.KY_PROCESS = @KY_PROCESS
		INNER JOIN ADM.VW_C_DATA_ACQUISITION_ORIGIN DA 
			ON DA.ID_DATA_ACQUISITION_ORIGIN =CF.ID_DATA_ACQUISITION_ORIGIN
		LEFT JOIN PRD.K_FORM KF 
			ON KF.ID_FORM = CF.ID_FORM 
			AND KF.KY_STATUS_FORM NOT IN ('CANCELLED') 
			AND ((
					@KY_PROCESS != 'QUALITY' 
					AND (@ID_PALLET IS NULL OR (KF.ID_PALLET = @ID_PALLET))
				) OR (
					@KY_PROCESS = 'QUALITY' 
					AND (@ID_PALLET IS NULL OR (KF.ID_INSPECTION_SKID = @ID_PALLET))
				)
			) AND (@ID_WORK_ORDER IS NULL OR (KF.ID_WORK_ORDER = @ID_WORK_ORDER)) 
			AND (@ID_FORM IS NULL OR (KF.ID_FORM = @ID_FORM)) 
			AND (@ID_PRODUCTION_LINE IS NULL OR (KF.ID_PRODUCTION_LINE = @ID_PRODUCTION_LINE))
			AND (@ID_K_FORM IS NULL OR (KF.ID_K_FORM = @ID_K_FORM))

