-- =============================================
-- Proyecto: Plaskolite
-- Copyright (c) - Acrux - 2017
-- Author: Julio Díaz
-- CRETAE date: 01/02/2019
-- Description: Inserts work order cartons
-- =============================================
-- 09/01/2019 IGP Added DS_CARTON field to insert in K_WORK_ORDER_CARTON table
-- =============================================


CREATE PROCEDURE    [PRD].[SPE_INSERT_WORK_ORDER_CARTONS]
	@XML_RESULT XML = '' OUT     -- --0 TO ERROR AND 1 TO CORRECT
	, @PIN_ID_WORK_ORDER INT
	, @PIN_ID_QA27 INT
	, @PIN_ID_CARTON INT
	, @PIN_NO_SCRAP INT
	, @PIN_NO_USAGE INT
	, @PIN_KY_AUTHORIZER_USER NVARCHAR(50)
	, @PIN_DS_AUTHORIZE NVARCHAR(500)
	, @PIN_KY_USER_APP NVARCHAR(50)
	, @PIN_NM_PROGRAM NVARCHAR(50)

AS 
BEGIN  
	----WE DECLARE THE STARTED VARIABLE THAT INDICATES IF WE WILL HAVE A TRANSACTION ON SPE

	BEGIN TRY
	
		DECLARE @V_EXIST_TRAN BIT = 0
			, @DT_SYSTEM DATETIME = GETDATE()
			, @ID_PRODUCTION_LINE INT
			, @ID_SHIFT INT
			, @ID_ITEM INT
			--WE VERIFY THAT EXISTS A WORKING TRANSACTION
		IF (@@TRANCOUNT = 0) BEGIN
				--IN CASE THAT THE TRANSACTION DOESNT START
			BEGIN TRANSACTION 
			--IT EDITS THE VARIABLE THAT INDICATES THAT THE TRANSACTION START IN THIS BLOCK TO CANCEL IN ANY MOMENT
			SET @V_EXIST_TRAN = 1

		END	

		SELECT TOP 1
			@ID_PRODUCTION_LINE = KS.ID_PRODUCTION_LINE
			, @ID_SHIFT = KS.ID_SHIFT
			, @ID_ITEM = KWO.ID_ITEM
		FROM PRD.K_QA27 KQ
			INNER JOIN PRD.K_SHIFT KS
				ON KQ.ID_SHIFT = KS.ID_SHIFT
				AND KQ.ID_QA27 = @PIN_ID_QA27
			INNER JOIN PRD.K_WORK_ORDER KWO
				ON KWO.ID_WORK_ORDER = KQ.ID_WORK_ORDER
				AND KWO.ID_WORK_ORDER = @PIN_ID_WORK_ORDER

		INSERT INTO PRD.K_WORK_ORDER_CARTON (
			ID_CARTON
			, KY_CARTON
			, NM_CARTON
			, DS_CARTON			
			, NO_USAGE
			, NO_SCRAP
			, ID_WORK_ORDER
			, ID_PRODUCTION_LINE
			, ID_QA27
			, ID_SHIFT
			, ID_ITEM
			, KY_AUTHORIZER_USER
			, DS_AUTHORIZE
			, DT_CREATION
			, KY_USER_APP_CREATION
			, NM_PROGAM_CREATE
		)
		SELECT @PIN_ID_CARTON
			, CVC.KY_VALUE_CATALOG
			, CVC.NM_VALUE_CATALOG
			, CVC.DS_VALUE_CATALOG
			, @PIN_NO_USAGE
			, @PIN_NO_SCRAP
			, @PIN_ID_WORK_ORDER
			, @ID_PRODUCTION_LINE
			, @PIN_ID_QA27
			, @ID_SHIFT
			, @ID_ITEM
			, @PIN_KY_AUTHORIZER_USER
			, @PIN_DS_AUTHORIZE
			, @DT_SYSTEM
			, @PIN_KY_USER_APP
			, @PIN_NM_PROGRAM
		FROM ADM.C_VALUE_CATALOG CVC
		WHERE CVC.ID_VALUE_CATALOG = @PIN_ID_CARTON

		-- WE BACK A RETURN VARIABLE THAT INDICATES ALL WAS PERFORMED OKAY 
		SET @XML_RESULT = DBO.F_ERROR_CREATE_HEADER( @@ROWCOUNT, 1, 'SUCCESSFUL')
		SET @XML_RESULT = DBO.F_ERROR_INSERT_MESSAGES(@XML_RESULT, 'Proceso exitoso', 'ES')
		SET @XML_RESULT = DBO.F_ERROR_INSERT_MESSAGES(@XML_RESULT, 'Successful Process', 'EN')

			-- IF THERE IS A TRANSACTION IN THIS BLOCK, IT WILL BE ERASED
		IF (@@TRANCOUNT > 0 AND @V_EXIST_TRAN = 1)
			COMMIT	
	END TRY
	BEGIN CATCH		
		--IF IT OCCURS A ERROR IN THIS BLOCK THE TRANSACTIO GET CANCELED
		IF (@@TRANCOUNT > 0 AND @V_EXIST_TRAN = 1)
			ROLLBACK
			
		SET @XML_RESULT = DBO.F_ERROR_MESSAGES(ERROR_NUMBER(), ERROR_MESSAGE())

	END CATCH
END

