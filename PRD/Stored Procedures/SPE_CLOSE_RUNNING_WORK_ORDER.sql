-- =============================================
-- Proyecto: Plaskolite
-- Copyright (c) - Acrux - 2017
-- Author: Julio Díaz
-- Create date: 11/29/2018
-- Description: Close a running Work Order
-- =============================================

CREATE PROCEDURE  [PRD].[SPE_CLOSE_RUNNING_WORK_ORDER]
	@XML_RESULT XML = '' OUT
	, @PIN_ID_WORK_ORDER INT
	, @PIN_KY_AUTHORIZER_USER NVARCHAR(50)
	, @PIN_DS_EXPLANATION NVARCHAR(512)
	, @PIN_KY_USER_APP NVARCHAR (50)
	, @PIN_NM_PROGRAM AS nvarchar(50) -- PROGRAM WHERE THE USER SEND TO DELETE A REGISTER 
AS   
BEGIN

	BEGIN TRY		   			
		DECLARE @V_EXIST_TRAN BIT = 0
			  , @DT_SYSTEM DATETIME = GETDATE()
	          , @XML_DATA XML

		
		IF (@@TRANCOUNT = 0) BEGIN
			BEGIN TRANSACTION
			SET @V_EXIST_TRAN = 1
		END	

		DECLARE @ID_QA27 INT = (SELECT TOP 1 ID_QA27 FROM PRD.K_QA27 WHERE ID_WORK_ORDER = @PIN_ID_WORK_ORDER AND KY_STATUS = 'RUNNING' ORDER BY DT_INITIAL_TIME DESC)

		--FIRST, WE NEED TO SET THE PALLETS TO SKIPPED AND THE VALUE OF FG_SEND_FORM TO 0
		UPDATE PRD.K_PALLET
			SET FG_SEND_FORM = 0,
				KY_STATUS = 'ACCEPTED',
				DT_FINAL_OPERATION_TIME = @DT_SYSTEM,
				DT_FINAL_TIME = @DT_SYSTEM,
				DT_UPDATE = @DT_SYSTEM,
				KY_USER_APP_UPDATE = @PIN_KY_USER_APP,
				NM_PROGRAM_UPDATE = @PIN_NM_PROGRAM
		WHERE ID_WORK_ORDER = @PIN_ID_WORK_ORDER AND FG_SEND_FORM = 1

		UPDATE PRD.K_QA27
			SET KY_STATUS = 'COMPLETE',
				DT_FINAL_TIME = @DT_SYSTEM,
				DT_UPDATE = @DT_SYSTEM,
				KY_USER_APP_UPDATE = @PIN_KY_USER_APP,
				NM_PROGRAM_UPDATE = @PIN_NM_PROGRAM
		WHERE ID_QA27 = @ID_QA27

		--THEN, WE NEED TO UPDATE THE STATUS OF WORK ORDER
		UPDATE PRD.K_WORK_ORDER
			SET KY_STATUS = 'COMPLETE',
				DT_UPDATE = @DT_SYSTEM,
				KY_USER_APP_UPDATE = @PIN_KY_USER_APP,
				NM_PROGRAM_UPDATE = @PIN_NM_PROGRAM
		WHERE ID_WORK_ORDER = @PIN_ID_WORK_ORDER
				
		SET @XML_DATA = (SELECT (SELECT ID_PRODUCTION_LINE FROM PRD.K_WORK_ORDER WHERE  ID_WORK_ORDER= @PIN_ID_WORK_ORDER)  AS '@ID_PRODUCTION_LINE' FOR XML PATH('PRODLINE'))

		--WE RETURN A VARIABLE THAT INDICATES THAT EVERYTHING WAS PERFORMED OKAY.
		SET @XML_RESULT = DBO.F_ERROR_CREATE_HEADER( @@ROWCOUNT, 0, 'SUCCESSFUL')
		SET @XML_RESULT = DBO.F_ERROR_INSERT_MESSAGES(@XML_RESULT, 'Se eliminó el idioma satisfactoriamente', 'ES')
		SET @XML_RESULT = DBO.F_ERROR_INSERT_MESSAGES(@XML_RESULT, 'Successfully deleted position', 'EN')
		SET @XML_RESULT = DBO.F_ERROR_INSERT_DATA (@XML_RESULT, @XML_DATA)

		--IN THIS BLOCK ALL TRANSACTIONS WILL DELETED
		IF (@@TRANCOUNT > 0 AND @V_EXIST_TRAN = 1)
			COMMIT				
	END TRY
	BEGIN CATCH			
		--IF IT OCCURS A ERROR IN THIS BLOCK THE TRANSACTIO GET CANCELED
		IF (@@TRANCOUNT > 0 AND @V_EXIST_TRAN = 1)
			ROLLBACK
					SET @XML_RESULT = DBO.F_ERROR_MESSAGES(ERROR_NUMBER(), ERROR_MESSAGE())

	END CATCH	
END

