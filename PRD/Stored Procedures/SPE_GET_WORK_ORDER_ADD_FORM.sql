-- =============================================
-- Proyect: Plaskolite
-- Copyright (c) - Acrux - 2018
-- Author: Julio César Tavares
-- Create date: 08/11/2018
-- Description: get alL WORK ORDERS PER LINE.
-- =============================================
-- 12/08/2018 JDR The query is changed to return all those work orders that ran from previous shift and the one that is running now
-- =============================================
CREATE PROCEDURE    [PRD].[SPE_GET_WORK_ORDER_ADD_FORM] 
		@PIN_ID_PRODUCTION_LINE AS int = NULL

AS
BEGIN
	DECLARE @DT_INITIAL_DATE DATETIME = (
		SELECT TOP 1 DATEADD(HOUR, -12, DT_START_SHIFT)
		FROM PRD.K_SHIFT KS
		WHERE KS.ID_PRODUCTION_LINE = @PIN_ID_PRODUCTION_LINE
			AND KS.FG_STATUS = 1
	)

	SELECT KWO.NO_WORK_ORDER
		, KWO.ID_WORK_ORDER
		, KWO.KY_CUSTOMER
		, KWO.NM_CUSTOMER
		, CI.ID_ITEM
		, CI.KY_ITEM
		, CI.NM_ITEM
		, CPL.ID_PRODUCTION_LINE
		, CPL.KY_PRODUCTION_LINE
		, CPL.NM_PRODUCTION_LINE
		, KWO.NM_MATERIAL
		, KWO.NM_COLOR
		, (KWO.NO_RUN_QTY + KWO.NO_QTY_ADDED) AS NO_RUN_QTY
		, KWO.NO_LENGHT
		, KWO.NO_WIDTH
		, KWO.NO_THICKNESS
		, (KWO.NO_ASSIGNED_TIME + ISNULL(KWO.NO_STANDARD_TIME,0)) NO_ASSIGNED_TIME
		, KWO.DT_WORK_ORDER
		, KWO.KY_STATUS
		, KWO.NO_ORDER
	FROM PRD.K_WORK_ORDER KWO
		INNER JOIN PRD.C_ITEM CI 
			ON KWO.ID_ITEM = CI.ID_ITEM
		INNER JOIN PRD.C_PRODUCTION_LINE CPL 
			ON KWO.ID_PRODUCTION_LINE = CPL.ID_PRODUCTION_LINE
	WHERE KWO.ID_PRODUCTION_LINE = @PIN_ID_PRODUCTION_LINE
		AND (
			EXISTS (SELECT TOP 1 1 FROM PRD.K_QA27 KQ WHERE KQ.DT_INITIAL_TIME >= @DT_INITIAL_DATE AND KQ.ID_WORK_ORDER = KWO.ID_WORK_ORDER)
			OR KWO.KY_STATUS IN ('SCHEDULED')
		)
	ORDER BY KWO.KY_STATUS, KWO.NO_ORDER, KWO.ID_WORK_ORDER
END

