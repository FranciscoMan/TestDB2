-- =============================================
-- Proyecto: Plaskolite
-- Copyright (c) - Acrux - 2017
-- Author: Julio Díaz
-- CRETAE date: 21/05/2018
-- Description: Execute the insert or update process to metrics-transition relationship
-- =============================================
CREATE PROCEDURE    [PRD].[SPE_INSERT_METRICS_TRANSITION] 
		  @XML_RESULT XML OUT
    	, @PIN_XML_METRICS_TRANSITIONS AS XML
		, @PIN_KY_USER AS NVARCHAR(50)
		, @PIN_NM_PROGRAM AS NVARCHAR(50)
AS 
BEGIN  

	DECLARE @V_EXIST_TRAN BIT = 0
		,@DT_SYSTEM DATETIME = GETDATE()

    BEGIN TRY

		IF (@@TRANCOUNT = 0) BEGIN
			BEGIN TRANSACTION

			SET @V_EXIST_TRAN = 1
		END

		CREATE TABLE #T_METRICS_TRANSITIONS  (
			ID_TRANSITION INT
			, ID_METRICS INT
		)

		INSERT INTO #T_METRICS_TRANSITIONS (ID_METRICS, ID_TRANSITION)
		SELECT c.value('@ID_METRICS', 'INT')
			, c.value('@ID_TRANSITION', 'INT')
		FROM @PIN_XML_METRICS_TRANSITIONS.nodes('TRANSITIONS/TRANSITION') t(c)

		INSERT INTO PRD.C_METRICS_TRANSITION (
			ID_METRICS
			, ID_TRANSITION
			, DT_CREATION
			, KY_USER_APP_CREATION
			, NM_PROGRAM_CREATE
		)
		SELECT TMT.ID_METRICS
			, TMT.ID_TRANSITION
			, @DT_SYSTEM
			, @PIN_KY_USER
			, @PIN_NM_PROGRAM
		FROM #T_METRICS_TRANSITIONS TMT
		WHERE NOT EXISTS (SELECT TOP 1 1 FROM PRD.C_METRICS_TRANSITION CMT WHERE CMT.ID_METRICS = TMT.ID_METRICS AND CMT.ID_TRANSITION = TMT.ID_TRANSITION)


		SET @XML_RESULT = DBO.F_ERROR_CREATE_HEADER( @@ROWCOUNT, 1, 'SUCCESSFUL')
		SET @XML_RESULT = DBO.F_ERROR_INSERT_MESSAGES(@XML_RESULT, 'Proceso exitoso', 'ES')
		SET @XML_RESULT = DBO.F_ERROR_INSERT_MESSAGES(@XML_RESULT, 'Successful Process', 'EN')

		IF (@@TRANCOUNT > 0 AND @V_EXIST_TRAN = 1)
			COMMIT	
	END TRY
	BEGIN CATCH		
		--IF IT OCCURS A ERROR IN THIS BLOCK THE TRANSACTIO GET CANCELED
		IF (@@TRANCOUNT > 0 AND @V_EXIST_TRAN = 1)
			ROLLBACK
			
		SET @XML_RESULT = DBO.F_ERROR_MESSAGES(ERROR_NUMBER(), ERROR_MESSAGE())
	END CATCH
END

