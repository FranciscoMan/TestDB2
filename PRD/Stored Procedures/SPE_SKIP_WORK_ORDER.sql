-- =============================================
-- Proyecto: Plaskolite
-- Copyright (c) - Acrux - 2018
-- Author: Gabriel Vázquez Torres
-- CRETAE date: 28/05/2018
-- Description: SET THE WORK ORDER TO SKIPPED STATUS
-- =============================================
-- 11/22/2018 JDR The record is added for the history of skipped work order
-- =============================================

CREATE PROCEDURE    [PRD].[SPE_SKIP_WORK_ORDER]
	  @XML_RESULT XML = '' OUT	-- --0 TO ERROR AND 1 TO CORRECT
	, @PIN_ID_WORK_ORDER INT
	, @PIN_ID_ISSUE_CAUSE_SKIPPED INT
	, @PIN_KY_AUTHORIZER_USER NVARCHAR(50)
	, @PIN_DS_SKIP_EXPLANATION AS NVARCHAR(512)
	, @PIN_KY_USER_APP AS NVARCHAR(50)
	, @PIN_NM_PROGRAM AS NVARCHAR(50)


AS 
BEGIN  

   	BEGIN TRY
		----WE DECLARE THE STARTED VARIABLE THAT INDICATES IF WE WILL HAVE A TRANSACTION ON SPE
		DECLARE @V_EXIST_TRAN BIT = 0, @DT_SYSTEM DATETIME = GETDATE()

		IF @@TRANCOUNT = 0 BEGIN
			BEGIN TRANSACTION
			--IT EDITS THE VARIABLE THAT INDICATES THAT THE TRANSACTION START IN THIS BLOCK TO CANCEL IN ANY MOMENT
			SET @V_EXIST_TRAN = 1
		END	

		DECLARE @ID_QA27 INT = (SELECT TOP 1 ID_QA27 FROM PRD.K_QA27 WHERE ID_WORK_ORDER = @PIN_ID_WORK_ORDER AND KY_STATUS = 'RUNNING' ORDER BY DT_INITIAL_TIME DESC)

		--FIRST, WE NEED TO SET THE PALLETS TO SKIPPED AND THE VALUE OF FG_SEND_FORM TO 0
		UPDATE PRD.K_PALLET
			SET FG_SEND_FORM = 0,
				KY_STATUS = 'SKIPPED',
				DT_FINAL_OPERATION_TIME = @DT_SYSTEM,
				DT_FINAL_TIME = @DT_SYSTEM,
				DT_UPDATE = @DT_SYSTEM,
				KY_USER_APP_UPDATE = @PIN_KY_USER_APP,
				NM_PROGRAM_UPDATE = @PIN_NM_PROGRAM
		WHERE ID_WORK_ORDER = @PIN_ID_WORK_ORDER AND FG_SEND_FORM = 1

		UPDATE PRD.K_QA27
			SET KY_STATUS = 'SKIPPED',
				DT_FINAL_TIME = @DT_SYSTEM,
				DT_UPDATE = @DT_SYSTEM,
				KY_USER_APP_UPDATE = @PIN_KY_USER_APP,
				NM_PROGRAM_UPDATE = @PIN_NM_PROGRAM
		WHERE ID_QA27 = @ID_QA27

		--THEN, WE NEED TO UPDATE THE STATUS OF WORK ORDER
		UPDATE PRD.K_WORK_ORDER
			SET KY_STATUS = 'SKIPPED',
				ID_ISSUE_CAUSE_SKIPPED = @PIN_ID_ISSUE_CAUSE_SKIPPED,
				DT_UPDATE = @DT_SYSTEM,
				KY_USER_APP_UPDATE = @PIN_KY_USER_APP,
				NM_PROGRAM_UPDATE = @PIN_NM_PROGRAM
		WHERE ID_WORK_ORDER = @PIN_ID_WORK_ORDER

		INSERT INTO PRD.K_SKIPPED_WORK_ORDER_LOG (
			ID_WORK_ORDER
			, DS_EXPLANATION
			, ID_ISSUE
			, KY_AUTHORIZER_USER
			, ID_QA27
			, DT_CREATION
			, KY_USER_APP_CREATION
			, NM_PROGAM_CREATE
		)
		VALUES (
			@PIN_ID_WORK_ORDER
			, @PIN_DS_SKIP_EXPLANATION
			, @PIN_ID_ISSUE_CAUSE_SKIPPED
			, @PIN_KY_AUTHORIZER_USER
			, @ID_QA27
			, @DT_SYSTEM
			, @PIN_KY_USER_APP
			, @PIN_NM_PROGRAM
		)

		-- WE BACK A RETURN VARIABLE THAT INDICATES ALL WAS PERFORMED OKAY 
		SET @XML_RESULT = DBO.F_ERROR_CREATE_HEADER( @@ROWCOUNT, 1, 'SUCCESSFUL')
		SET @XML_RESULT = DBO.F_ERROR_INSERT_MESSAGES(@XML_RESULT, 'Proceso exitoso', 'ES')
		SET @XML_RESULT = DBO.F_ERROR_INSERT_MESSAGES(@XML_RESULT, 'Successful Process', 'EN')		

		-- IF THERE IS A TRANSACTION IN THIS BLOCK, IT WILL BE ERASED
		IF (@@TRANCOUNT > 0 AND @V_EXIST_TRAN = 1)
			COMMIT	

	END TRY
	BEGIN CATCH		
		--IF IT OCCURS A ERROR IN THIS BLOCK THE TRANSACTIO GET CANCELED
		IF (@@TRANCOUNT > 0 AND @V_EXIST_TRAN = 1)
			ROLLBACK
			
		DECLARE @KY_ERROR INT  = 	ERROR_NUMBER()
		DECLARE @ERROR_MESSAGE NVARCHAR(250)  = 	 ERROR_MESSAGE()
	
	    SET @XML_RESULT = DBO.F_ERROR_CREATE_HEADER( @@ROWCOUNT, @KY_ERROR, 'ERROR')
		SET @XML_RESULT = DBO.F_ERROR_MESSAGES( @KY_ERROR,'Ocurrió un error al procesar el registro')
		SET @XML_RESULT = DBO.F_ERROR_MESSAGES( @KY_ERROR, 'There was an error while processing the register.')
		
		EXECUTE ADM.SPE_RAISE_ERROR
			
	END CATCH
END

