-- =============================================
-- Proyect: Plaskolite
-- Copyright (c) - Acrux - 2017
-- Author: Julio Díaz
-- CREATE date: 11/07/2018
-- Description: Get tool change report
-- =============================================

CREATE PROCEDURE    [PRD].[SPE_GET_RPT_TOOL_CHANGE] 
		@BP INT
AS
BEGIN

	DECLARE @T_WORK_ORDER TABLE (
		ID_WORK_ORDER INT
		,ID_BRANCH_PLANT INT
		, KY_STATUS_WORK_ORDER NVARCHAR(50)
		, ID_PRODUCTION_LINE INT
		, NM_PRODUCTION_LINE NVARCHAR(300)
		, NO_ASSIGNED_TIME INT
		, NO_ORDER INT
		, NO_MINUTES_BEFORE_START INT DEFAULT (0)
		, KY_ITEM NVARCHAR(50)
		, NM_CUSTOMER NVARCHAR(200)
		, XML_TRANSITIONS XML
	)

	INSERT INTO @T_WORK_ORDER (
		ID_WORK_ORDER
		,ID_BRANCH_PLANT
		, ID_PRODUCTION_LINE
		, NM_PRODUCTION_LINE
		, NO_ORDER
		, NO_ASSIGNED_TIME
		, KY_ITEM
		, NM_CUSTOMER
		, KY_STATUS_WORK_ORDER
	)
	SELECT KWO.ID_WORK_ORDER,
	KWO.ID_BRANCH_PLANT
		, KWO.ID_PRODUCTION_LINE
		, CPL.NM_PRODUCTION_LINE
		, KWO.NO_ORDER
		, KWO.NO_ASSIGNED_TIME
		, CI.KY_ITEM
		, KWO.NM_CUSTOMER
		, KWO.KY_STATUS
	FROM PRD.K_WORK_ORDER KWO
		INNER JOIN PRD.C_PRODUCTION_LINE CPL	
			ON KWO.ID_PRODUCTION_LINE = CPL.ID_PRODUCTION_LINE
		INNER JOIN PRD.C_ITEM CI
			ON KWO.ID_ITEM = CI.ID_ITEM
	WHERE KWO.KY_STATUS = 'SCHEDULED'
		AND EXISTS (SELECT TOP 1 1 FROM PRD.K_WORK_ORDER_TRANSITIONS KWOT INNER JOIN PRD.C_TRANSITION CT ON CT.ID_TRANSITION = KWOT.ID_TRANSITION AND CT.FG_IS_TOOL_CHANGE = 1 AND KWO.ID_WORK_ORDER = KWOT.ID_WORK_ORDER) AND KWO.ID_BRANCH_PLANT = @BP

	; WITH T_TIME_PREVIOUS_WORK_ORDER AS (
		SELECT TWO.ID_WORK_ORDER
			, TWO.ID_PRODUCTION_LINE
			, SUM(NO_PRODUCTION_TIME) AS NO_SUM_MINUTES
		FROM @T_WORK_ORDER TWO
			INNER JOIN PRD.K_WORK_ORDER KWO
				ON TWO.NO_ORDER > KWO.NO_ORDER
				AND TWO.ID_PRODUCTION_LINE = KWO.ID_PRODUCTION_LINE
				AND KWO.KY_STATUS = 'SCHEDULED'
		GROUP BY TWO.ID_WORK_ORDER, TWO.ID_PRODUCTION_LINE
	)

	UPDATE TWO
	SET NO_MINUTES_BEFORE_START = NO_SUM_MINUTES
	FROM @T_WORK_ORDER TWO
		INNER JOIN T_TIME_PREVIOUS_WORK_ORDER TTPWO
			ON TWO.ID_WORK_ORDER = TTPWO.ID_WORK_ORDER

	UPDATE TWO
	SET XML_TRANSITIONS = (
			SELECT NM_TRANSITION AS li
			FROM PRD.K_WORK_ORDER_TRANSITIONS WOT
			WHERE WOT.ID_WORK_ORDER = TWO.ID_WORK_ORDER
			FOR XML RAW (''), ROOT ('ul'), ELEMENTS
		) 
	FROM @T_WORK_ORDER TWO

	SELECT ID_WORK_ORDER
	,ID_BRANCH_PLANT
		, KY_STATUS_WORK_ORDER
		, ID_PRODUCTION_LINE
		, NM_PRODUCTION_LINE
		, NO_ASSIGNED_TIME
		, NO_ORDER
		, NO_MINUTES_BEFORE_START
		, DATEADD(MINUTE, NO_MINUTES_BEFORE_START, GETDATE()) AS DT_ESTIMATED_START
		, NM_CUSTOMER
		, XML_TRANSITIONS
		, KY_ITEM
	FROM @T_WORK_ORDER
	ORDER BY DATEADD(MINUTE, NO_MINUTES_BEFORE_START, GETDATE())

END

